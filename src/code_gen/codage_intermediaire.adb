with TEXT_IO;
use  TEXT_IO;
				--------------------
	package body		CODAGE_INTERMEDIAIRE
				--------------------
is

  DEBUG				: BOOLEAN	:= TRUE;

  DESIGNATION_DISPO			: DESIGNATION;
  DESIGNATION_T			: TEMPORAIRE_T	:= 0;
  DESIGNATION_A			: TEMPORAIRE_A	:= 0;

  TABLE_DESIGNATIONS	: array( DESIGNATION ) of REC_DESIGNATION;

  LAST_LBL			: TARGET_LBL;
  LAST_BRANCH			: NUM_BRANCH;
  DERNIERE_REPRISE_CALL		: TARGET_LBL	:= 0;


			--===========--
  function		NEW_DESIGNATION		return DESIGNATION
  is
    ALLOUEE	: DESIGNATION;
  begin

    if DESIGNATION_DISPO = 0 or else DESIGNATION_T = NMAX_TEMPORAIRES_T
    then raise TROP_DE_DESIGNATIONS;
    end if;

    ALLOUEE           := DESIGNATION_DISPO;
    DESIGNATION_DISPO := DESIGNATION( - TABLE_DESIGNATIONS( ALLOUEE ).VAR_OR_CST.VALEUR );

    DESIGNATION_T     := DESIGNATION_T + 1;
    TABLE_DESIGNATIONS( ALLOUEE ).VAR_OR_CST := (GENRE =>TMP, TMP_T =>DESIGNATION_T);
    TABLE_TEMPORAIRES_T( DESIGNATION_T ).LOCALISATIONS := 0;
    return ALLOUEE;

  end	NEW_DESIGNATION;
	--===========--



			--============--
  procedure		FREE_DESIGNATION		( D :DESIGNATION )
  is
  begin

    if (D <= 0) Or (D > NMAX_DESIGNATIONS) then raise DESIGNATION_INVALIDE; end if;

    TABLE_DESIGNATIONS( D ).VAR_OR_CST := ( Genre =>IMM, VALEUR => - INTEGER( DESIGNATION_DISPO ) );
    DESIGNATION_DISPO := D;

  end	FREE_DESIGNATION;
	--============--



			--===--
  function 		NEW_LBL			return TARGET_LBL is
  begin
    if LAST_LBL = MAX_TARGET_LBLS then raise TROP_DE_REPRISES; end if;

    LAST_LBL := LAST_LBL + 1;
    return LAST_LBL;

  end	NEW_LBL;
	--===--



			--====--
  procedure		STOCK_CP		( FOR_LBL :TARGET_LBL )
  is
  begin
    TARGET_ILOC( FOR_LBL ) := COMPTEUR_PROGRAMME;

    if DEBUG then
      PUT_LINE( INSTR_LOC'IMAGE( COMPTEUR_PROGRAMME ) & " <<" & TARGET_LBL'IMAGE( FOR_LBL ) & ">>" );
    end if;
  end	STOCK_CP;
	--====--



			--====--
  procedure		CODE_IMM		( DESIGNE :DESIGNATION; VALEUR_IMM : INTEGER )
  is
  begin
    DESIGNATION_T := DESIGNATION_T - 1;
    TABLE_DESIGNATIONS( DESIGNE ).VAR_OR_CST := ( GENRE => IMM, VALEUR => VALEUR_IMM );

  end	CODE_IMM;
	--====--



			--====--
  procedure		CODE_CST		( DESIGNE :DESIGNATION; CST_DEF :TREE )
  is
  begin
    DESIGNATION_T := DESIGNATION_T - 1;
    TABLE_DESIGNATIONS( DESIGNE ).VAR_OR_CST := ( Genre => CST, REFERENCE => CST_DEF );

  end	CODE_CST;
	--====--


			--====--
  procedure		CODE_PRM		( PARAMETRE :DESIGNATION; DIRECTION :DIRECTION_DE_PASSAGE )
  is
  begin
    TABLE_INSTRUCTIONS( COMPTEUR_PROGRAMME ) := ( GENRE		=> PRM,
					PARAMETRE		=> TABLE_DESIGNATIONS( PARAMETRE ),
					FIN_BLOC		=> FALSE,
					BLOC_ENTREE_LATERALE => 0, BLOC_SORTIE_LATERALE => 0 );
    TABLE_INSTRUCTIONS( COMPTEUR_PROGRAMME ).PARAMETRE.VAR_OR_CST := ( PRM, DIRECTION );
    COMPTEUR_PROGRAMME := COMPTEUR_PROGRAMME + 1;

  end	CODE_PRM;
	--====--


			--=====--
  procedure		CODE_ADR1		( RESULTAT :DESIGNATION; OP: OPCI_ADR1; X1: DESIGNATION )
  is
  begin
    if TABLE_DESIGNATIONS( RESULTAT ).VAR_OR_CST.GENRE = TMP then

      if DESIGNATION_A = NMAX_TEMPORAIRES_A then raise TROP_DE_TEMPORAIRES_A; end if;

      DESIGNATION_T := DESIGNATION_T - 1;
      DESIGNATION_A := DESIGNATION_A + 1;
      TABLE_DESIGNATIONS( RESULTAT ).VAR_OR_CST := ( GENRE => ADR, TMP_A => DESIGNATION_A );

      TABLE_TEMPORAIRES_A( DESIGNATION_A ).LOCALISATIONS := 0;
      TABLE_TEMPORAIRES_A( DESIGNATION_A ).PARAMETRES.GENRE := INDIRECT;
      TABLE_TEMPORAIRES_A( DESIGNATION_A ).PARAMETRES.DEPLACEMENT := 0;

    end if;

    TABLE_INSTRUCTIONS( COMPTEUR_PROGRAMME ) := (	GENRE		=> ADR1, ADR1_OP => OP,
					ADR1_X1		=> TABLE_DESIGNATIONS( X1 ),
					ADR1_RESULT	=> TABLE_DESIGNATIONS( RESULTAT ),
					FIN_BLOC		=> FALSE,
					BLOC_ENTREE_LATERALE => 0, BLOC_SORTIE_LATERALE => 0 );

    COMPTEUR_PROGRAMME := COMPTEUR_PROGRAMME + 1;

  end	CODE_ADR1;
	--=====--


			--======--
  procedure		CODE_FLOT0	( OP :OPCI_FLOT0; ALLOC_DESALLOC :INTEGER := 0 )
  is 
  begin

    TABLE_INSTRUCTIONS( COMPTEUR_PROGRAMME ) := (	GENRE		=> FLOT0, FLOT0_OP =>OP,
					ALLOC_DESALLOC	=> ALLOC_DESALLOC,
					FIN_BLOC		=> FALSE,
					BLOC_ENTREE_LATERALE => 0, BLOC_SORTIE_LATERALE => 0 );

    if DEBUG then
      PUT( INSTR_LOC'IMAGE( COMPTEUR_PROGRAMME ) & ASCII.HT & OPCI_FLOT0'IMAGE( OP ) );
      if OP /= UNLINK then PUT( ' ' & INTEGER'IMAGE( ALLOC_DESALLOC ) ); end if;
      NEW_LINE;
    end if;

    COMPTEUR_PROGRAMME := COMPTEUR_PROGRAMME + 1;

  end	CODE_FLOT0;
	--======--


			--======--
  procedure		CODE_FLOT1	( OP :OPCI_FLOT1; TARGET :TARGET_LBL )
  is
  begin

    if LAST_BRANCH = MAX_BRANCHS then raise TROP_IFLOTS1; end if;

    LAST_BRANCH := LAST_BRANCH + 1;
    BRANCH_ILOC( LAST_BRANCH ) := COMPTEUR_PROGRAMME;

    TABLE_INSTRUCTIONS( COMPTEUR_PROGRAMME ) := (	GENRE		=> FLOT1, FLOT1_OP =>OP,
					FLOT1_SAUT	=> TARGET,
					FIN_BLOC		=> FALSE,
					BLOC_ENTREE_LATERALE => 0, BLOC_SORTIE_LATERALE => 0 );

    if DEBUG then
      PUT_LINE( INSTR_LOC'IMAGE( COMPTEUR_PROGRAMME ) & ASCII.HT & OPCI_FLOT1'IMAGE( OP ) & " <<" & TARGET_LBL'IMAGE( TARGET ) & ">>" );
    end if;

    COMPTEUR_PROGRAMME := COMPTEUR_PROGRAMME + 1;

  end	CODE_FLOT1;
	--======--




begin
  for INDICE in DESIGNATION loop
    TABLE_DESIGNATIONS( INDICE ).VAR_OR_CST := ( GENRE => IMM, VALEUR => INTEGER( -INDICE + 1 ) );
  end loop;
  DESIGNATION_DISPO := NMAX_DESIGNATIONS;

end	CODAGE_INTERMEDIAIRE;
	--------------------