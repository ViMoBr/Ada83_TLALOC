separate (IDL.SEM_PHASE)
    --|----------------------------------------------------------------------------------------------
    --| DEF_WALK
    --|----------------------------------------------------------------------------------------------
package body DEF_WALK is
  use DEF_UTIL;
  use VIS_UTIL;
  use MAKE_NOD;
  use NOD_WALK;
  use EXP_TYPE, EXPRESO;
  use REQ_UTIL;
  use SET_UTIL;
  use GEN_SUBS;

  function COPY_COMP_LIST_IDS (COMP_LIST : TREE; H : H_TYPE) return TREE;
  function COPY_ITEM_S_IDS (ITEM_S : TREE; H : H_TYPE) return TREE;
  procedure WALK_COMP_LIST (COMP_LIST : TREE; H : H_TYPE);

  procedure PRINT_REAL (R : TREE) is -- $$$$ DEBUG ONLY
  begin
    if R.TY = DN_REAL_VAL then
      PRINT_TREE (D (XD_NUMER, R));
      Put ("/");
      PRINT_TREE (D (XD_DENOM, R));
    else
      PRINT_TREE (R);
    end if;
  end PRINT_REAL;

        -- $$$$ SHOULDN'T BE HERE
  function GET_SUBSTRUCT (TYPE_SPEC : TREE) return TREE is
    SUBTYPE_SPEC : TREE;
  begin
    if TYPE_SPEC.TY in CLASS_PRIVATE_SPEC then
      SUBTYPE_SPEC := D (SM_TYPE_SPEC, TYPE_SPEC);
      if SUBTYPE_SPEC /= TREE_VOID then
        return SUBTYPE_SPEC;
      end if;
    elsif TYPE_SPEC.TY = DN_INCOMPLETE then
      SUBTYPE_SPEC := D (XD_FULL_TYPE_SPEC, TYPE_SPEC);
      if SUBTYPE_SPEC /= TREE_VOID then
        return SUBTYPE_SPEC;
      end if;
    end if;
    return TYPE_SPEC;
  end GET_SUBSTRUCT;

		-------------
  function	EVAL_TYPE_DEF		( TYPE_DEF :TREE; ID :TREE; H :H_TYPE;
					  DSCRMT_DECL_S :TREE := TREE_VOID)	return TREE
  is		-------------

    TYPE_SPEC         : TREE := TREE_VOID;
    BASE_TYPE         : TREE := TREE_VOID;
    RECORD_REGION_DEF : TREE;
  begin

    if TYPE_DEF = TREE_VOID then
      return TREE_VOID;
    end if;

-- put_line( "; def_walk.EVAL_TYPE_DEF ligne 62 ID.TY= " & NODE_NAME'IMAGE( ID.TY ) & "  ID= " & PRINT_NAME( D(LX_SYMREP, ID ) ) );


                -- GET BASE TYPE IN CASE IT IS PRIVATE, L_PRIVATE OR INCOMPLETE
    if ID.TY = DN_TYPE_ID and then D( SM_FIRST, ID ) /= ID then
      BASE_TYPE := D (SM_TYPE_SPEC, D (SM_FIRST, ID));
    end if;

    case CLASS_TYPE_DEF'(TYPE_DEF.TY) is

                        -- FOR AN ENUMERATION_TYPE_DECLARATION
      when DN_ENUMERATION_DEF =>
        declare
          ENUM_LITERAL_S     : constant TREE := D (AS_ENUM_LITERAL_S, TYPE_DEF);
          ENUM_LITERAL_LIST  : SEQ_TYPE      := LIST (ENUM_LITERAL_S);
          ENUM_LITERAL       : TREE;
          DEF, PRIOR_DEF     : TREE;
          ENUM_LITERAL_COUNT : Integer       := 0;
          ENUM_HEADER        : TREE;
          PRIVATE_SPEC       : TREE          := D (SM_TYPE_SPEC, ID);
        begin

                                        -- CREATE THE ENUMERATION NODE
          TYPE_SPEC := MAKE_ENUMERATION (SM_LITERAL_S => ENUM_LITERAL_S, XD_SOURCE_NAME => ID);
          if BASE_TYPE = TREE_VOID then
            BASE_TYPE := TYPE_SPEC;
          end if;
          D (SM_BASE_TYPE, TYPE_SPEC, BASE_TYPE);

                                        -- INSERT NAMES IN ENVIRONMENT
          WALK_SOURCE_NAME_S (ENUM_LITERAL_S, H);

                                        -- MAKE A HEADER FOR THE DEF NODES FOR THE ENUM LITERALS
          ENUM_HEADER := MAKE_FUNCTION_SPEC (AS_NAME => MAKE_USED_NAME_ID (LX_SYMREP => TREE_VOID, SM_DEFN => ID), AS_PARAM_S => MAKE_PARAM_S (LIST => (TREE_NIL, TREE_NIL)));

                                        -- STORE TYPE SPEC IN TYPE SPEC IN TYPE ID
                                        -- ... (NEEDED FOR GET_PRIOR_HOMOGRAPH_DEF)
          D (SM_TYPE_SPEC, ID, BASE_TYPE);

                                        -- FOR EACH ENUM_LITERAL
          while not IS_EMPTY (ENUM_LITERAL_LIST) loop
            POP (ENUM_LITERAL_LIST, ENUM_LITERAL);

                                                -- STORE THE BASE TYPE
            D (SM_OBJ_TYPE, ENUM_LITERAL, BASE_TYPE);

                                                -- ASSIGN SM_POS AND DEFAULT SM_REP ATTRIBUTES
            DI (SM_POS, ENUM_LITERAL, ENUM_LITERAL_COUNT);
            DI (SM_REP, ENUM_LITERAL, ENUM_LITERAL_COUNT);
            ENUM_LITERAL_COUNT := ENUM_LITERAL_COUNT + 1;

                                                -- MAKE DEF VISIBLE FOR THIS ENUM LITERAL
            DEF := GET_DEF_FOR_ID (ENUM_LITERAL);
            MAKE_DEF_VISIBLE (DEF, ENUM_HEADER);

                                                -- CHECK FOR UNIQUENESS
            PRIOR_DEF := GET_PRIOR_HOMOGRAPH_DEF (DEF);
            if PRIOR_DEF /= TREE_VOID and then (not IS_OVERLOADABLE_HEADER (D (XD_HEADER, DEF)) or else EXPRESSION_TYPE_OF_DEF (DEF) = TYPE_SPEC) then
              ERROR (D (LX_SRCPOS, TYPE_DEF), "ENUM LITERAL IS DUPLICATE - " & PRINT_NAME (D (LX_SYMREP, ENUM_LITERAL)));
              MAKE_DEF_IN_ERROR (DEF);
            end if;
          end loop;

                                        -- CONSTRUCT SM_RANGE ATTRIBUTE FOR THE ENUMERATION NODE
          D (SM_RANGE, TYPE_SPEC,
            MAKE_RANGE
             (SM_TYPE_SPEC => TYPE_SPEC, AS_EXP1 => MAKE_USED_OBJECT_ID (SM_EXP_TYPE => TYPE_SPEC, SM_DEFN => HEAD (LIST (ENUM_LITERAL_S)), LX_SYMREP => D (LX_SYMREP, HEAD (LIST (ENUM_LITERAL_S))), SM_VALUE => UARITH.U_VAL (0)),
              AS_EXP2      => MAKE_USED_OBJECT_ID (SM_EXP_TYPE => TYPE_SPEC, SM_DEFN => ENUM_LITERAL, LX_SYMREP => D (LX_SYMREP, ENUM_LITERAL), SM_VALUE => UARITH.U_VAL (ENUM_LITERAL_COUNT - 1))));

                                        -- COMPUTE SIZE AND STORE IN ENUMERATION NODE
          if ENUM_LITERAL_COUNT > 2**8 then
            DI (CD_IMPL_SIZE, TYPE_SPEC, 16);
          else
            DI (CD_IMPL_SIZE, TYPE_SPEC, 8);
          end if;

                                        -- RESTORE TYPE SPEC IN TYPE SPEC IN TYPE ID
                                        -- ... (NO LONGER NEEDED FOR GET_PRIOR_HOMOGRAPH_DEF)
          if PRIVATE_SPEC /= TREE_VOID then
            D (SM_TYPE_SPEC, ID, PRIVATE_SPEC);
          end if;
        end;

                        -- FOR A SUBTYPE INDICATION
      when DN_SUBTYPE_INDICATION =>
        declare
          SUBTYPE_INDICATION	: TREE	:= TYPE_DEF;
          BASE_TYPE		: TREE	:= EVAL_SUBTYPE_INDICATION( SUBTYPE_INDICATION );
        begin
                                        -- RESOLVE SUBTYPE INDICATION AND GET ITS SUBTYPE
          RESOLVE_SUBTYPE_INDICATION( SUBTYPE_INDICATION, TYPE_SPEC );

                                        -- RETURN WITHOUT MODIFYING BASE TYPE, ETC.
          return TYPE_SPEC;
        end;

                        -- FOR AN INTEGER_TYPE_DEFINITION
      when DN_INTEGER_DEF =>
        declare
          use UARITH;

          RANGE_NODE : constant TREE := D (AS_CONSTRAINT, TYPE_DEF);
          EXP1       : TREE          := D (AS_EXP1, RANGE_NODE);
          EXP2       : TREE          := D (AS_EXP2, RANGE_NODE);

          TYPESET_1   : TYPESET_TYPE;
          TYPESET_2   : TYPESET_TYPE;
          LOWER_BOUND : TREE;
          UPPER_BOUND : TREE;

          ANCESTOR_TYPE : TREE;
          DERIVED_BASE  : TREE;
        begin

                                        -- EVALUATE THE LOWER BOUND EXPRESSION
          EVAL_EXP_TYPES (EXP1, TYPESET_1);
          REQUIRE_INTEGER_TYPE (EXP1, TYPESET_1);
          REQUIRE_UNIQUE_TYPE (EXP1, TYPESET_1);
          EXP1 := RESOLVE_EXP (EXP1, TYPESET_1);
          D (AS_EXP1, RANGE_NODE, EXP1);
          LOWER_BOUND := GET_STATIC_VALUE (EXP1);

                                        -- EVALUATE THE UPPER BOUND EXPRESSION
          EVAL_EXP_TYPES (EXP2, TYPESET_2);
          REQUIRE_INTEGER_TYPE (EXP2, TYPESET_2);
          REQUIRE_UNIQUE_TYPE (EXP2, TYPESET_2);
          EXP2 := RESOLVE_EXP (EXP2, TYPESET_2);
          D (AS_EXP2, RANGE_NODE, EXP2);
          UPPER_BOUND := GET_STATIC_VALUE (EXP2);

                                        -- IF BOTH BOUNDS ARE STATIC
          if LOWER_BOUND /= TREE_VOID and UPPER_BOUND /= TREE_VOID then

                                                -- IF RANGE FITS WITHIN SHORT_INTEGER
            if LOWER_BOUND >= PREDEFINED_SHORT_INTEGER_FIRST and UPPER_BOUND <= PREDEFINED_SHORT_INTEGER_LAST and PREDEFINED_SHORT_INTEGER /= TREE_VOID then
                                                        -- USE SHORT_INTEGER
              ANCESTOR_TYPE := PREDEFINED_SHORT_INTEGER;

                                                        -- ELSE IF RANGE FITS WITHIN INTEGER
            elsif LOWER_BOUND >= PREDEFINED_INTEGER_FIRST and UPPER_BOUND <= PREDEFINED_INTEGER_LAST then

                                                        -- USE INTEGER
              ANCESTOR_TYPE := PREDEFINED_INTEGER;

                                                        -- ELSE IF RANGE FITS WITHIN LONG_INTEGER
            elsif LOWER_BOUND >= PREDEFINED_LONG_INTEGER_FIRST and UPPER_BOUND <= PREDEFINED_LONG_INTEGER_LAST then

                                                        -- USE LONG_INTEGER
              ANCESTOR_TYPE := PREDEFINED_LONG_INTEGER;

                                                        -- ELSE -- SINCE NOT WITHIN ANY PREDEFINED INTEGER TYPE
            else

                                                        -- REPORT ERROR
              ERROR (D (LX_SRCPOS, RANGE_NODE), "INTEGER TYPE TOO LARGE FOR IMPLEMENTATION");

                                                        -- ASSUME LARGEST INTEGER TYPE
              ANCESTOR_TYPE := PREDEFINED_LARGEST_INTEGER;
            end if;

                                                -- ELSE -- SINCE AT LEAST ONE BOUND IS NOT STATIC
          else

                                                -- ASSUME LARGEST INTEGER TYPE
            ANCESTOR_TYPE := PREDEFINED_LARGEST_INTEGER;

                                                -- IF LOWER BOUND IS NOT STATIC
                                                -- ... AND A TYPE WAS DETERMINED FOR IT
            if LOWER_BOUND = TREE_VOID and then not IS_EMPTY (TYPESET_1) then

                                                        -- INDICATE ERROR
              ERROR (D (LX_SRCPOS, EXP1), "BOUNDS MUST BE STATIC");
            end if;

                                                -- IF UPPER BOUND IS NOT STATIC
                                                -- ... AND A TYPE WAS DETERMINED FOR IT
            if UPPER_BOUND = TREE_VOID and then not IS_EMPTY (TYPESET_2) then

                                                        -- INDICATE ERROR
              ERROR (D (LX_SRCPOS, EXP2), "BOUNDS MUST BE STATIC");
            end if;
          end if;

                                        -- CONSTRUCT ANONYMOUS DERIVED INTEGER TYPE
          DERIVED_BASE := COPY_NODE (ANCESTOR_TYPE);
          if BASE_TYPE = TREE_VOID then
            BASE_TYPE := DERIVED_BASE;
          end if;
          D (SM_BASE_TYPE, DERIVED_BASE, BASE_TYPE);
          D (XD_SOURCE_NAME, DERIVED_BASE, ID);
          DB (SM_IS_ANONYMOUS, DERIVED_BASE, True);
          D (SM_DERIVED, DERIVED_BASE, ANCESTOR_TYPE);

                                        -- CONSTRUCT SUBTYPE OF ANONYMOUS TYPE
          TYPE_SPEC := COPY_NODE (DERIVED_BASE);
          DB (SM_IS_ANONYMOUS, TYPE_SPEC, False);
          D (SM_DERIVED, TYPE_SPEC, TREE_VOID);
          D (SM_RANGE, TYPE_SPEC, RANGE_NODE);

                                        -- MAKE RANGE TYPE THE NEW BASE TYPE
          D (SM_TYPE_SPEC, RANGE_NODE, BASE_TYPE);
        end;

                        -- FOR A FLOATING_POINT_CONSTRAINT USED AS REAL_TYPE_DEFINITION
      when DN_FLOAT_DEF =>
        declare
          use UARITH;

          CONSTRAINT : constant TREE := D (AS_CONSTRAINT, TYPE_DEF);
          EXP        : TREE          := D (AS_EXP, CONSTRAINT);
          RANGE_NODE : constant TREE := D (AS_RANGE, CONSTRAINT);
          EXP1       : TREE;
          EXP2       : TREE;

          TYPESET     : TYPESET_TYPE;
          TYPESET_1   : TYPESET_TYPE;
          TYPESET_2   : TYPESET_TYPE;
          ACCURACY    : TREE;
          LOWER_BOUND : TREE;
          UPPER_BOUND : TREE;

          ANCESTOR_TYPE : TREE;
          DERIVED_BASE  : TREE;
        begin

                                        -- EVALUATE THE ACCURACY EXPRESSION
          EVAL_EXP_TYPES (EXP, TYPESET);
          REQUIRE_INTEGER_TYPE (EXP, TYPESET);
          REQUIRE_UNIQUE_TYPE (EXP, TYPESET);
          EXP := RESOLVE_EXP (EXP, TYPESET);
          D (AS_EXP, CONSTRAINT, EXP);
          ACCURACY := GET_STATIC_VALUE (EXP);

                                        -- IF A RANGE IS GIVEN
          if RANGE_NODE /= TREE_VOID then

                                                -- EVALUATE THE LOWER BOUND EXPRESSION
            EXP1 := D (AS_EXP1, RANGE_NODE);
            EVAL_EXP_TYPES (EXP1, TYPESET_1);
            REQUIRE_REAL_TYPE (EXP1, TYPESET_1);
            REQUIRE_UNIQUE_TYPE (EXP1, TYPESET_1);
            EXP1 := RESOLVE_EXP (EXP1, TYPESET_1);
            D (AS_EXP1, RANGE_NODE, EXP1);
            LOWER_BOUND := GET_STATIC_VALUE (EXP1);

                                                -- EVALUATE THE UPPER BOUND EXPRESSION
            EXP2 := D (AS_EXP2, RANGE_NODE);
            EVAL_EXP_TYPES (EXP2, TYPESET_2);
            REQUIRE_REAL_TYPE (EXP2, TYPESET_2);
            REQUIRE_UNIQUE_TYPE (EXP2, TYPESET_2);
            EXP2 := RESOLVE_EXP (EXP2, TYPESET_2);
            D (AS_EXP2, RANGE_NODE, EXP2);
            UPPER_BOUND := GET_STATIC_VALUE (EXP2);
          end if;

                                        -- IF ACCURACY AND BOTH BOUNDS (IF GIVEN) ARE STATIC
                                        -- AND ACCURACY IS POSITIVE
          if ACCURACY /= TREE_VOID and then (RANGE_NODE = TREE_VOID or else (LOWER_BOUND /= TREE_VOID and then UPPER_BOUND /= TREE_VOID)) and then not (ACCURACY <= U_VAL (0)) then

                                                -- IF RANGE FITS WITHIN FLOAT
            if ACCURACY <= PREDEFINED_FLOAT_ACCURACY and then (RANGE_NODE = TREE_VOID or else (LOWER_BOUND >= PREDEFINED_FLOAT_FIRST and then UPPER_BOUND <= PREDEFINED_FLOAT_LAST)) then

                                                        -- USE FLOAT
              ANCESTOR_TYPE := PREDEFINED_FLOAT;

                                                        -- IF RANGE FITS WITHIN LONG_FLOAT
            elsif ACCURACY <= PREDEFINED_LONG_FLOAT_ACCURACY and then (RANGE_NODE = TREE_VOID or else (LOWER_BOUND >= PREDEFINED_LONG_FLOAT_FIRST and then UPPER_BOUND <= PREDEFINED_LONG_FLOAT_LAST)) then

                                                        -- USE LONG_FLOAT
              ANCESTOR_TYPE := PREDEFINED_LONG_FLOAT;

                                                        -- ELSE -- SINCE TOO LARGE FOR IMPLEMENTATION
            else

                                                        -- REPORT ERROR
              ERROR (D (LX_SRCPOS, CONSTRAINT), "FLOATING TYPE TOO LARGE FOR IMPLEMENTATION");

                                                        -- ASSUME LARGEST FLOATING TYPE
              ANCESTOR_TYPE := PREDEFINED_LARGEST_FLOAT;
            end if;

                                                -- ELSE -- SINCE ACCURACY OR AT LEAST ONE BOUND IS NOT STATIC
          else

                                                -- ASSUME LARGEST FLOATING TYPE
            ANCESTOR_TYPE := PREDEFINED_LARGEST_FLOAT;

                                                -- IF ACCURACY IS NOT STATIC
                                                -- ... AND A TYPE WAS DETERMINED FOR IT
            if ACCURACY = TREE_VOID and then not IS_EMPTY (TYPESET) then

                                                        -- INDICATE ERROR
              ERROR (D (LX_SRCPOS, EXP), "ACCURACY MUST BE STATIC");
            end if;

                                                -- IF A RANGE WAS GIVEN
            if RANGE_NODE /= TREE_VOID then

                                                        -- IF LOWER BOUND IS NOT STATIC
                                                        -- ... AND A TYPE WAS DETERMINED FOR IT
              if LOWER_BOUND = TREE_VOID and then not IS_EMPTY (TYPESET_1) then

                                                                -- INDICATE ERROR
                ERROR (D (LX_SRCPOS, EXP1), "BOUNDS MUST BE STATIC");
              end if;

                                                        -- IF UPPER BOUND IS NOT STATIC
                                                        -- ... AND A TYPE WAS DETERMINED FOR IT
              if UPPER_BOUND = TREE_VOID and then not IS_EMPTY (TYPESET_2) then

                                                                -- INDICATE ERROR
                ERROR (D (LX_SRCPOS, EXP2), "BOUNDS MUST BE STATIC");
              end if;
            end if;
          end if;

                                        -- CONSTRUCT ANONYMOUS DERIVED FLOATING TYPE
          DERIVED_BASE := COPY_NODE (ANCESTOR_TYPE);
          if BASE_TYPE = TREE_VOID then
            BASE_TYPE := DERIVED_BASE;
          end if;
          D (SM_BASE_TYPE, DERIVED_BASE, BASE_TYPE);
          D (XD_SOURCE_NAME, DERIVED_BASE, ID);
          DB (SM_IS_ANONYMOUS, DERIVED_BASE, True);
          D (SM_DERIVED, DERIVED_BASE, ANCESTOR_TYPE);

                                        -- MAKE RANGE TYPE THE NEW BASE TYPE
          D (SM_TYPE_SPEC, CONSTRAINT, BASE_TYPE);

                                        -- CONSTRUCT SUBTYPE OF ANONYMOUS TYPE
          TYPE_SPEC := COPY_NODE (DERIVED_BASE);
          DB (SM_IS_ANONYMOUS, TYPE_SPEC, False);
          D (SM_DERIVED, TYPE_SPEC, TREE_VOID);
          D (SM_ACCURACY, TYPE_SPEC, ACCURACY);
          D (SM_TYPE_SPEC, CONSTRAINT, BASE_TYPE);
          if RANGE_NODE /= TREE_VOID then
            D (SM_RANGE, TYPE_SPEC, RANGE_NODE);
            D (SM_TYPE_SPEC, RANGE_NODE, BASE_TYPE);
          else
            D (SM_RANGE, TYPE_SPEC, D (SM_RANGE, ANCESTOR_TYPE));
          end if;
        end;

                        -- FOR A FIXED_POINT_CONSTRAINT USED AS REAL_TYPE_DEFINITION
      when DN_FIXED_DEF =>
        declare
          use UARITH;

          CONSTRAINT : constant TREE := D (AS_CONSTRAINT, TYPE_DEF);
          EXP        : TREE          := D (AS_EXP, CONSTRAINT);
          RANGE_NODE : constant TREE := D (AS_RANGE, CONSTRAINT);
          EXP1       : TREE;
          EXP2       : TREE;

          TYPESET     : TYPESET_TYPE;
          TYPESET_1   : TYPESET_TYPE;
          TYPESET_2   : TYPESET_TYPE;
          ACCURACY    : TREE;
          LOWER_BOUND : TREE;
          UPPER_BOUND : TREE;

                  --ANCESTOR_TYPE:  TREE;
          DERIVED_BASE : TREE;
          POWER_31     : constant TREE := U_VAL (2)**U_VAL (31);
        begin

                                        -- EVALUATE THE ACCURACY EXPRESSION
          EVAL_EXP_TYPES (EXP, TYPESET);
          REQUIRE_REAL_TYPE (EXP, TYPESET);
          REQUIRE_UNIQUE_TYPE (EXP, TYPESET);
          EXP := RESOLVE_EXP (EXP, TYPESET);
          D (AS_EXP, CONSTRAINT, EXP);
          ACCURACY := GET_STATIC_VALUE (EXP);

                                        -- IF ACCURACY IS NOT STATIC
          if ACCURACY = TREE_VOID then

                                                -- PUT OUT ERROR MESSAGE
            ERROR (D (LX_SRCPOS, EXP), "FIXED ACCURACY MUST BE STATIC");

                                                -- ELSE IF ACCURACY IS NOT POSITIVE
          elsif D (XD_NUMER, ACCURACY) <= U_VAL (0) then

                                                -- PUT OUT ERROR MESSAGE
            ERROR (D (LX_SRCPOS, EXP), "FIXED ACCURACY MUST POSITIVE");

                                                -- AND PRETEND IT WAS NOT STATIC (FOR LATER TESTS)
            ACCURACY := TREE_VOID;
          end if;

                                        -- IF NO RANGE IS GIVEN
          if RANGE_NODE = TREE_VOID then

                                                -- INDICATE ERROR
            ERROR (D (LX_SRCPOS, CONSTRAINT), "RANGE REQUIRED");

                                                -- AND RETURN VOID TO INDICATE ERROR TO CALLER
            return TREE_VOID;
          end if;

                                        -- EVALUATE THE LOWER BOUND EXPRESSION
          EXP1 := D (AS_EXP1, RANGE_NODE);
          EVAL_EXP_TYPES (EXP1, TYPESET_1);
          REQUIRE_REAL_TYPE (EXP1, TYPESET_1);
          REQUIRE_UNIQUE_TYPE (EXP1, TYPESET_1);
          EXP1 := RESOLVE_EXP (EXP1, TYPESET_1);
          D (AS_EXP1, RANGE_NODE, EXP1);
          LOWER_BOUND := GET_STATIC_VALUE (EXP1);

                                        -- EVALUATE THE UPPER BOUND EXPRESSION
          EXP2 := D (AS_EXP2, RANGE_NODE);
          EVAL_EXP_TYPES (EXP2, TYPESET_2);
          REQUIRE_REAL_TYPE (EXP2, TYPESET_2);
          REQUIRE_UNIQUE_TYPE (EXP2, TYPESET_2);
          EXP2 := RESOLVE_EXP (EXP2, TYPESET_2);
          D (AS_EXP2, RANGE_NODE, EXP2);
          UPPER_BOUND := GET_STATIC_VALUE (EXP2);

                                        -- IF RANGE AND BOUNDS ARE NOT STATIC
          if ACCURACY = TREE_VOID or LOWER_BOUND = TREE_VOID or UPPER_BOUND = TREE_VOID then

                                                -- IF LOWER BOUND IS NOT STATIC
            if LOWER_BOUND = TREE_VOID then

                                                        -- PUT OUT ERROR MESSAGE
              ERROR (D (LX_SRCPOS, EXP1), "LOWER BOUND MUST BE STATIC");

                                                        -- IF UPPER BOUND IS NOT STATIC
            end if;
            if UPPER_BOUND = TREE_VOID then

                                                        -- PUT OUT ERROR MESSAGE
              ERROR (D (LX_SRCPOS, EXP1), "UPPER BOUND MUST BE STATIC");

                                                        -- AND RETURN VOID TO INDICATE ERROR TO CALLER
            end if;
            return TREE_VOID;
          end if;

                                        -- IF BOUNDS FIT WITHIN 32-BIT FIXED TYPE
          if UPPER_BOUND <= ACCURACY * POWER_31 and LOWER_BOUND >= -ACCURACY * POWER_31 - ACCURACY then

                                                -- USE GIVEN BOUNDS
            null;

                                                -- ELSE -- SINCE BOUNDS DO NOT FIT
          else

                                                -- PUT OUT ERROR MESSAGE
            ERROR (D (LX_SRCPOS, CONSTRAINT), "FIXED TYPE TOO LARGE");

                                                -- RETURN VOID TO INDICATE ERROR
            return TREE_VOID;
          end if;

                                        -- CONSTRUCT ANONYMOUS FIXED TYPE
          DERIVED_BASE :=
           MAKE_FIXED
            (XD_SOURCE_NAME => ID, SM_IS_ANONYMOUS => True,
             SM_RANGE       =>
              MAKE_RANGE
               (SM_TYPE_SPEC => BASE_TYPE, AS_EXP1 => MAKE_USED_OBJECT_ID (LX_SYMREP => TREE_VOID, SM_VALUE => -POWER_31 * ACCURACY, SM_EXP_TYPE => BASE_TYPE),
                AS_EXP2      => MAKE_USED_OBJECT_ID (LX_SYMREP => TREE_VOID, SM_VALUE => POWER_31 * ACCURACY - ACCURACY, SM_EXP_TYPE => BASE_TYPE)),
             CD_IMPL_SIZE   => 32, SM_ACCURACY => ACCURACY, CD_IMPL_SMALL => ACCURACY);
          if BASE_TYPE = TREE_VOID then
            BASE_TYPE := DERIVED_BASE;
          end if;
          D (SM_BASE_TYPE, DERIVED_BASE, BASE_TYPE);

                                        -- CONSTRUCT SUBTYPE OF ANONYMOUS TYPE
          TYPE_SPEC := COPY_NODE (DERIVED_BASE);
          DB (SM_IS_ANONYMOUS, TYPE_SPEC, False);
          D (SM_DERIVED, TYPE_SPEC, TREE_VOID);
          D (SM_RANGE, TYPE_SPEC, RANGE_NODE);
          D (SM_ACCURACY, TYPE_SPEC, ACCURACY);

                                        -- MAKE RANGE TYPE THE NEW BASE TYPE
          D (SM_TYPE_SPEC, CONSTRAINT, BASE_TYPE);
          D (SM_TYPE_SPEC, RANGE_NODE, BASE_TYPE);
          D (SM_TYPE_SPEC, D (SM_RANGE, DERIVED_BASE), BASE_TYPE);
        end;

                        -- FOR A CONSTRAINED_ARRAY_DEFINITION
      when DN_CONSTRAINED_ARRAY_DEF =>
        declare
          SUBTYPE_INDICATION : TREE := D (AS_SUBTYPE_INDICATION, TYPE_DEF);
          CONSTRAINT         : TREE := D (AS_CONSTRAINT, TYPE_DEF);

          COMP_TYPE : TREE;

          INDEX_EXP_LIST  : SEQ_TYPE := LIST (D (AS_DISCRETE_RANGE_S, CONSTRAINT));
          INDEX_EXP       : TREE;
          INDEX_BASE_TYPE : TREE;
          INDEX_SUBTYPE   : TREE;
                  --INDEX_TYPE_MARK:TREE;

          TYPESET : TYPESET_TYPE;

          DISCRETE_RANGE_LIST : SEQ_TYPE := (TREE_NIL, TREE_NIL);
          INDEX_LIST          : SEQ_TYPE := (TREE_NIL, TREE_NIL);
          SCALAR_LIST         : SEQ_TYPE := (TREE_NIL, TREE_NIL);
        begin
          COMP_TYPE := EVAL_SUBTYPE_INDICATION (SUBTYPE_INDICATION);
          RESOLVE_SUBTYPE_INDICATION (SUBTYPE_INDICATION, COMP_TYPE);

          while not IS_EMPTY (INDEX_EXP_LIST) loop
            POP (INDEX_EXP_LIST, INDEX_EXP);
            EVAL_NON_UNIVERSAL_DISCRETE_RANGE (INDEX_EXP, TYPESET);
            REQUIRE_UNIQUE_TYPE (INDEX_EXP, TYPESET);
            INDEX_BASE_TYPE     := GET_THE_TYPE (TYPESET);
            INDEX_EXP           := RESOLVE_DISCRETE_RANGE (INDEX_EXP, INDEX_BASE_TYPE);
            DISCRETE_RANGE_LIST := APPEND (DISCRETE_RANGE_LIST, INDEX_EXP);

            if INDEX_BASE_TYPE /= TREE_VOID then
              INDEX_SUBTYPE := GET_SUBTYPE_OF_DISCRETE_RANGE (INDEX_EXP);
            else
              INDEX_SUBTYPE := TREE_VOID;
            end if;

            SCALAR_LIST := APPEND (SCALAR_LIST, INDEX_SUBTYPE);
            INDEX_LIST  := APPEND (INDEX_LIST, MAKE_INDEX (AS_NAME => TREE_VOID, SM_TYPE_SPEC => INDEX_SUBTYPE));
          end loop;
          LIST (D (AS_DISCRETE_RANGE_S, CONSTRAINT), DISCRETE_RANGE_LIST);

          BASE_TYPE := MAKE_ARRAY (SM_COMP_TYPE => COMP_TYPE, SM_INDEX_S => MAKE_INDEX_S (LIST => INDEX_LIST), SM_IS_ANONYMOUS => True, XD_SOURCE_NAME => ID);
          D (SM_BASE_TYPE, BASE_TYPE, BASE_TYPE);

          TYPE_SPEC := MAKE_CONSTRAINED_ARRAY (SM_INDEX_SUBTYPE_S => MAKE_SCALAR_S (LIST => SCALAR_LIST), SM_BASE_TYPE => BASE_TYPE);

--  MODIF V.MORIN 18/6/2025 pour DN_COMPONENT_ID
--

put_line( "; def_walk ligne 591 ID.TY= " & NODE_NAME'IMAGE( ID.TY ) );

          if  ID.TY = DN_VARIABLE_ID or ID.TY = DN_COMPONENT_ID  then						-- IF THIS DEF WAS PART OF A VARIABLE DECLARATION
            DB( SM_IS_ANONYMOUS, TYPE_SPEC, TRUE );							-- MARK TYPE_SPEC ANONYMOUS
          end if;
        end;

                        -- FOR AN UNCONSTRAINED ARRAY DEFINITION
      when DN_UNCONSTRAINED_ARRAY_DEF =>
        declare
          SUBTYPE_INDICATION : TREE := D (AS_SUBTYPE_INDICATION, TYPE_DEF);
          INDEX_S            : TREE := D (AS_INDEX_S, TYPE_DEF);

          COMP_TYPE  : TREE;
          INDEX_LIST : SEQ_TYPE := LIST (INDEX_S);
          INDEX      : TREE;
          TYPE_MARK  : TREE;
                  --TYPE_DEFN: TREE;

          ERROR_SEEN : Boolean := False;
        begin

                                        -- EVALUATE COMPONENT TYPE
          COMP_TYPE := EVAL_SUBTYPE_INDICATION (SUBTYPE_INDICATION);
          RESOLVE_SUBTYPE_INDICATION( SUBTYPE_INDICATION, COMP_TYPE );

                                        -- REMEMBER IF IN ERROR
          if COMP_TYPE = TREE_VOID then
            ERROR_SEEN := True;
          end if;

                                        -- FOR EACH INDEX
          while not IS_EMPTY (INDEX_LIST) loop
            POP (INDEX_LIST, INDEX);

                                                -- EVALUATE THE TYPE MARK
            TYPE_MARK := D (AS_NAME, INDEX);
            TYPE_MARK := WALK_TYPE_MARK (TYPE_MARK);
            D (AS_NAME, INDEX, TYPE_MARK);

                                                -- IF TYPE MARK WAS ACCEPTED
            if TYPE_MARK /= TREE_VOID then

                                                        --STORE INDEX SUBTYPE IN INDEX NODE
              D (SM_TYPE_SPEC, INDEX, D (SM_TYPE_SPEC, GET_NAME_DEFN (TYPE_MARK)));
                                                        --$$$$ ???? CHECK THIS

                                                        -- ELSE -- SINCE TYPE MARK WAS IN ERROR
            else

                                                        -- REMEMBER THAT THERE WAS AN ERROR
              ERROR_SEEN := True;
            end if;
          end loop;

                                        -- IF DEFINITION WAS CORRECT
          if not ERROR_SEEN then

                                                -- MAKE ARRAY NODE
            TYPE_SPEC := MAKE_ARRAY (SM_COMP_TYPE => COMP_TYPE, SM_INDEX_S => INDEX_S);

                                                -- MAKE SURE IT IS ITS OWN BASE TYPE
            BASE_TYPE := TYPE_SPEC;
          end if;
        end;

                        -- FOR AN ACCESS TYPE DEFINITION
      when DN_ACCESS_DEF =>
        declare
          SUBTYPE_INDICATION : TREE := D (AS_SUBTYPE_INDICATION, TYPE_DEF);

          DESIG_TYPE : TREE;
        begin
                                        -- EVALUATE THE DESIGNATED TYPE
          DESIG_TYPE := EVAL_SUBTYPE_INDICATION (SUBTYPE_INDICATION);
          RESOLVE_SUBTYPE_INDICATION (SUBTYPE_INDICATION, DESIG_TYPE);

                                        -- IF DESIGNATED TYPE DECLARATION WAS CORRECT
          if DESIG_TYPE /= TREE_VOID then

                                                -- CONSTRUCT AN ACCESS NODE
            TYPE_SPEC := MAKE_ACCESS (SM_DESIG_TYPE => DESIG_TYPE, XD_SOURCE_NAME => ID);
            BASE_TYPE := TYPE_SPEC;
            D (SM_BASE_TYPE, TYPE_SPEC, TYPE_SPEC);

                                                -- IF SUBTYPE INDICATION CONTAINS A CONSTRAINT
                                                -- $$$$ WORRY ABOUT CONSTRAINED DESIG TYPE

            if SUBTYPE_INDICATION.TY = DN_SUBTYPE_INDICATION and then D (AS_CONSTRAINT, SUBTYPE_INDICATION) /= TREE_VOID then

                                                        -- CONSTRUCT A CONSTRAINED_ACCESS NODE
              TYPE_SPEC := MAKE_CONSTRAINED_ACCESS (SM_DESIG_TYPE => DESIG_TYPE, SM_BASE_TYPE => TYPE_SPEC, XD_SOURCE_NAME => ID);

            end if;
          end if;
        end;

      when DN_DERIVED_DEF =>
        declare
          SUBTYPE_INDICATION	: TREE	:= D( AS_SUBTYPE_INDICATION, TYPE_DEF );

          PARENT_SUBTYPE	: TREE;
          PARENT_TYPE	: TREE;

          SUBTYPE_SPEC	: TREE;
        begin

--put_line( "DEF_WALK 694 SUBTYPE_INDICATION" );
--print_nod.print_node( SUBTYPE_INDICATION );

                                        -- EVALUATE THE PARENT TYPE
          PARENT_TYPE := EVAL_SUBTYPE_INDICATION( SUBTYPE_INDICATION );
          PARENT_TYPE := GET_BASE_STRUCT( PARENT_TYPE );
          RESOLVE_SUBTYPE_INDICATION( SUBTYPE_INDICATION, PARENT_SUBTYPE );

--put_line( "DEF_WALK 702 PARENT_SUBTYPE" );
--print_nod.print_node( PARENT_TYPE );

                                        -- CHECK THAT PARENT TYPE IS DERIVABLE AT THIS POINT
          if PARENT_TYPE.TY not in CLASS_DERIVABLE_SPEC then
            if PARENT_TYPE.TY = DN_INCOMPLETE and then D( XD_FULL_TYPE_SPEC, PARENT_TYPE ) /= TREE_VOID then
              null;
            else
              ERROR( D( LX_SRCPOS, SUBTYPE_INDICATION ), "TYPE IS NOT DERIVABLE HERE" );
              PARENT_TYPE := TREE_VOID;
            end if;
          end if;

                                        -- IF PARENT TYPE DECLARATION WAS NOT CORRECT
          if PARENT_TYPE = TREE_VOID then
            return TREE_VOID;
          end if;

--          BASE_TYPE := D( SM_BASE_TYPE, PARENT_TYPE );

                                        -- MAKE DERIVED TYPE SPEC
          TYPE_SPEC := COPY_NODE( GET_BASE_STRUCT( PARENT_TYPE ) );
          D( XD_SOURCE_NAME, TYPE_SPEC, ID );
          if BASE_TYPE = TREE_VOID then
            BASE_TYPE := TYPE_SPEC;
          end if;
          D( SM_DERIVED, TYPE_SPEC, PARENT_TYPE );

--put_line( "DEF_WALK 732 TYPE_SPEC" );
--print_nod.print_node( TYPE_SPEC );



                                        -- IF TYPE IS AN ENUMERATION TYPE (AND NOT GENERIC (<>))
          if TYPE_SPEC.TY = DN_ENUMERATION and then D (SM_LITERAL_S, TYPE_SPEC) /= TREE_VOID then

                                                -- COPY THE ENUMERATION LITERALS
            declare
              ENUM_LITERAL_LIST : SEQ_TYPE := LIST (D (SM_LITERAL_S, TYPE_SPEC));
              ENUM_LITERAL      : TREE;
              NEW_LIST          : SEQ_TYPE := (TREE_NIL, TREE_NIL);
              TEMP_DEF          : TREE;

              ENUM_HEADER : TREE;
            begin
                                                        -- MAKE A HEADER FOR THE DEF NODES FOR THE ENUM LITERALS
              ENUM_HEADER := MAKE_FUNCTION_SPEC (AS_NAME => MAKE_USED_NAME_ID (LX_SYMREP => TREE_VOID, SM_DEFN => ID), AS_PARAM_S => MAKE_PARAM_S (LIST => (TREE_NIL, TREE_NIL)));

                                                        -- FOR EACH LITERAL
              while not IS_EMPTY (ENUM_LITERAL_LIST) loop
                POP (ENUM_LITERAL_LIST, ENUM_LITERAL);

                                                                -- MAKE A NEW COPY OF IT
                ENUM_LITERAL := COPY_NODE (ENUM_LITERAL);
                if D (LX_SYMREP, ENUM_LITERAL).TY = DN_SYMBOL_REP then
                  TEMP_DEF := MAKE_DEF_FOR_ID (ENUM_LITERAL, H);
                  MAKE_DEF_VISIBLE (TEMP_DEF, ENUM_HEADER);
                else
                  D (XD_REGION, ENUM_LITERAL, D (XD_SOURCE_NAME, H.REGION_DEF));
                end if;
                D (LX_SRCPOS, ENUM_LITERAL, TREE_VOID);
                D (SM_OBJ_TYPE, ENUM_LITERAL, BASE_TYPE);
                NEW_LIST := APPEND (NEW_LIST, ENUM_LITERAL);
              end loop;

              D (SM_LITERAL_S, TYPE_SPEC, MAKE_ENUM_LITERAL_S (LIST => NEW_LIST));
            end;

                                                -- ELSE IF TYPE IS A RECORD OR TASK TYPE
          elsif TYPE_SPEC.TY = DN_RECORD or else TYPE_SPEC.TY = DN_TASK_SPEC then

            declare
              H         : H_TYPE := EVAL_TYPE_DEF.H;
              S         : S_TYPE;
              NODE_HASH : NODE_HASH_TYPE;

            begin

                                                        -- ENTER RECORD DECLARATIVE REGION
              RECORD_REGION_DEF := GET_DEF_FOR_ID( ID );
              ENTER_REGION( RECORD_REGION_DEF, H, S );


--put_line( "DEF_WALK 787 TYPE_SPEC" );
--print_nod.print_node( TYPE_SPEC );

                                                        -- COPY THE RECORD STRUCTURE USING GENERIC SUBSTITUTION
              SUBSTITUTE_ATTRIBUTES( TYPE_SPEC, NODE_HASH, H );


--put_line( "DEF_WALK 794 TYPE_SPEC" );
--print_nod.print_node( TYPE_SPEC );

                                                        -- LEAVE RECORD DECLARATIVE REGION
              LEAVE_REGION( RECORD_REGION_DEF, S );
            end;

                                                -- ELSE IF TYPE IS [LIMITED] PRIVATE
          elsif TYPE_SPEC.TY in CLASS_PRIVATE_SPEC then

            declare
              H : H_TYPE := EVAL_TYPE_DEF.H;
              S : S_TYPE;
            begin

                                                        -- KILL FULL TYPE SPEC (SINCE DERIVED IS PRIVATE)
              D (SM_TYPE_SPEC, TYPE_SPEC, TREE_VOID);

                                                        -- ENTER RECORD DECLARATIVE REGION
              RECORD_REGION_DEF := GET_DEF_FOR_ID (ID);
              ENTER_REGION (RECORD_REGION_DEF, H, S);

                                                        -- COPY THE DISCRIMINANT NAMES
              D (SM_DISCRIMINANT_S, TYPE_SPEC, COPY_ITEM_S_IDS (D (SM_DISCRIMINANT_S, TYPE_SPEC), H));

                                                        -- LEAVE RECORD DECLARATIVE REGION
              LEAVE_REGION (RECORD_REGION_DEF, S);
            end;

                                                -- ELSE IF TYPE IS ARRAY
          elsif TYPE_SPEC.TY = DN_ARRAY then

                                                -- USE TYPE AS BASE TYPE, EVEN FOR PRIVATE
            BASE_TYPE := TYPE_SPEC;
          end if;

                                        -- IF PARENT TYPE HAS A CONSTRAINT
          if PARENT_SUBTYPE /= PARENT_TYPE then

                                                -- MAKE THE NEW BASE TYPE ANONYMOUS
            DB (SM_IS_ANONYMOUS, TYPE_SPEC, True);

                                                -- FIX UP BASE TYPE OF TYPE SPEC
            if BASE_TYPE.TY in CLASS_NON_TASK then
              D (SM_BASE_TYPE, TYPE_SPEC, BASE_TYPE);
            end if;

                                                -- COPY THE PARENT SUBTYPE
            SUBTYPE_SPEC := COPY_NODE (PARENT_SUBTYPE);

                                                -- FIX UP SUBTYPE NODE
            D( XD_SOURCE_NAME, SUBTYPE_SPEC, ID );

                                                -- REPLACE RESULT WITH SUBTYPE
            TYPE_SPEC := SUBTYPE_SPEC;
          end if;


--put_line( "DEF_WALK 852 TYPE_SPEC" );
--print_nod.print_node( TYPE_SPEC );
                                        -- ADD BASE TYPE AND SOURCE NAME
                                        -- (DONE AGAIN AFTER THE CASE STATEMENT; NEEDED FOR DERIV SUBP)
          if TYPE_SPEC.TY in CLASS_NON_TASK then
            D (SM_BASE_TYPE, TYPE_SPEC, BASE_TYPE);
          end if;


--put_line( "DEF_WALK 861 TYPE_SPEC" );
--print_nod.print_node( TYPE_SPEC );



          D (XD_SOURCE_NAME, TYPE_SPEC, ID);

                                        -- CREATE DERIVED SUBPROGRAMS
          LIST (TYPE_DEF, DERIVED.MAKE_DERIVED_SUBPROGRAM_LIST (GET_BASE_TYPE (TYPE_SPEC), GET_BASE_TYPE (PARENT_SUBTYPE), H));
        end;

      when DN_RECORD_DEF =>
        declare
          COMP_LIST : constant TREE := D (AS_COMP_LIST, TYPE_DEF);

          H : H_TYPE := EVAL_TYPE_DEF.H;
          S : S_TYPE;
        begin
          TYPE_SPEC := MAKE_RECORD (XD_SOURCE_NAME => ID, SM_DISCRIMINANT_S => DSCRMT_DECL_S, SM_COMP_LIST => COMP_LIST);

                                        -- ENTER RECORD DECLARATIVE REGION
          RECORD_REGION_DEF := GET_DEF_FOR_ID (ID);
          ENTER_REGION (RECORD_REGION_DEF, H, S);

                                        -- WALK THE COMPONENT LIST
          WALK_COMP_LIST (COMP_LIST, H);

                                        -- LEAVE RECORD DECLARATIVE REGION
          LEAVE_REGION (RECORD_REGION_DEF, S);
        end;

      when DN_PRIVATE_DEF =>
        TYPE_SPEC := MAKE_PRIVATE;
        D (SM_DISCRIMINANT_S, TYPE_SPEC, DSCRMT_DECL_S);

      when DN_L_PRIVATE_DEF =>
        TYPE_SPEC := MAKE_L_PRIVATE;
        D (SM_DISCRIMINANT_S, TYPE_SPEC, DSCRMT_DECL_S);

      when DN_FORMAL_DSCRT_DEF =>
        TYPE_SPEC := MAKE_ENUMERATION (SM_LITERAL_S => MAKE_ENUM_LITERAL_S ((TREE_NIL, TREE_NIL)));

      when DN_FORMAL_INTEGER_DEF =>
        TYPE_SPEC := MAKE_INTEGER;

      when DN_FORMAL_FIXED_DEF =>
        TYPE_SPEC := MAKE_FIXED;

      when DN_FORMAL_FLOAT_DEF =>
        TYPE_SPEC := MAKE_FLOAT;

    end case;

                -- IF TYPE DEFINITION WAS IN ERROR
    if TYPE_SPEC = TREE_VOID then

                        -- RETURN VOID TO INDICATE ERROR
      return TREE_VOID;
    end if;

                -- ADD BASE TYPE AND SOURCE NAME
    if BASE_TYPE = TREE_VOID then
      BASE_TYPE := TYPE_SPEC;
    end if;
    if TYPE_SPEC.TY in CLASS_NON_TASK then
      D (SM_BASE_TYPE, TYPE_SPEC, BASE_TYPE);
    end if;
    D (XD_SOURCE_NAME, TYPE_SPEC, ID);


--put_line( "DEF_WALK 921 TYPE_SPEC" );
--print_nod.print_node( TYPE_SPEC );


                -- RETURN THE CONSTRUCTED TYPE_SPEC
    return TYPE_SPEC;
  end EVAL_TYPE_DEF;

  function COPY_COMP_LIST_IDS (COMP_LIST : TREE; H : H_TYPE) return TREE is
    DECL_S           : TREE     := D (AS_DECL_S, COMP_LIST);
    VARIANT_PART     : TREE     := D (AS_VARIANT_PART, COMP_LIST);
    NEW_COMP_LIST    : TREE     := COPY_NODE (COMP_LIST);
    VARIANT_S        : TREE;
    VARIANT_LIST     : SEQ_TYPE;
    VARIANT          : TREE;
    NEW_VARIANT_LIST : SEQ_TYPE := (TREE_NIL, TREE_NIL);
  begin
    D (LX_SRCPOS, NEW_COMP_LIST, TREE_VOID);

    DECL_S := COPY_ITEM_S_IDS (DECL_S, H);
    D (AS_DECL_S, NEW_COMP_LIST, DECL_S);

    if VARIANT_PART /= TREE_VOID then
      VARIANT_PART := COPY_NODE (VARIANT_PART);
      D (LX_SRCPOS, VARIANT_PART, TREE_VOID);
      D (AS_VARIANT_PART, NEW_COMP_LIST, VARIANT_PART);
      VARIANT_S := COPY_NODE (D (AS_VARIANT_S, VARIANT_PART));
      D (LX_SRCPOS, VARIANT_S, TREE_VOID);
      D (AS_VARIANT_S, VARIANT_PART, VARIANT_S);
      VARIANT_LIST := LIST (VARIANT_S);

      while not IS_EMPTY (VARIANT_LIST) loop
        POP (VARIANT_LIST, VARIANT);
        if VARIANT.TY = DN_VARIANT then
          VARIANT := COPY_NODE (VARIANT);
          D (LX_SRCPOS, VARIANT, TREE_VOID);
          D (AS_COMP_LIST, VARIANT, COPY_COMP_LIST_IDS (D (AS_COMP_LIST, VARIANT), H));
          NEW_VARIANT_LIST := APPEND (NEW_VARIANT_LIST, VARIANT);
        end if;
      end loop;
      LIST (VARIANT_S, NEW_VARIANT_LIST);
    end if;

    D (AS_PRAGMA_S, NEW_COMP_LIST, TREE_VOID);

    return NEW_COMP_LIST;
  end COPY_COMP_LIST_IDS;

  function COPY_ITEM_S_IDS (ITEM_S : TREE; H : H_TYPE) return TREE is
    NEW_ITEM_S    : TREE;
    ITEM_LIST     : SEQ_TYPE;
    ITEM          : TREE;
    NEW_ITEM_LIST : SEQ_TYPE := (TREE_NIL, TREE_NIL);

    SOURCE_NAME_S : TREE;
    ID_LIST       : SEQ_TYPE;
    ID            : TREE;
    ID_DEF        : TREE;
    NEW_ID_LIST   : SEQ_TYPE;
  begin
    if ITEM_S = TREE_VOID then
      return TREE_VOID;
    end if;

    NEW_ITEM_S := COPY_NODE (ITEM_S);
    ITEM_LIST  := LIST (NEW_ITEM_S);

    D (LX_SRCPOS, NEW_ITEM_S, TREE_VOID);
    while not IS_EMPTY (ITEM_LIST) loop
      POP (ITEM_LIST, ITEM);
      if ITEM.TY in CLASS_DSCRMT_PARAM_DECL'FIRST .. CLASS_ID_S_DECL'LAST then
        ITEM := COPY_NODE (ITEM);
        D (LX_SRCPOS, ITEM, TREE_VOID);
        NEW_ITEM_LIST := APPEND (NEW_ITEM_LIST, ITEM);

        SOURCE_NAME_S := COPY_NODE (D (AS_SOURCE_NAME_S, ITEM));
        D (LX_SRCPOS, SOURCE_NAME_S, TREE_VOID);
        D (AS_SOURCE_NAME_S, ITEM, SOURCE_NAME_S);

        ID_LIST     := LIST (SOURCE_NAME_S);
        NEW_ID_LIST := (TREE_NIL, TREE_NIL);
        while not IS_EMPTY (ID_LIST) loop
          POP (ID_LIST, ID);
          ID := COPY_NODE (ID);
          D (LX_SRCPOS, ID, TREE_VOID);
          if D (LX_SYMREP, ID).TY = DN_SYMBOL_REP then
            ID_DEF := MAKE_DEF_FOR_ID (ID, H);
            MAKE_DEF_VISIBLE (ID_DEF);
          else
            D (XD_REGION, ID, H.REGION_DEF);
          end if;
          NEW_ID_LIST := APPEND (NEW_ID_LIST, ID);
        end loop;
        LIST (SOURCE_NAME_S, NEW_ID_LIST);

      elsif ITEM.TY = DN_NULL_COMP_DECL then
        NEW_ITEM_LIST := APPEND (NEW_ITEM_LIST, ITEM);
      end if;

    end loop;
    LIST (NEW_ITEM_S, NEW_ITEM_LIST);
    return NEW_ITEM_S;
  end COPY_ITEM_S_IDS;

  function GET_SUBTYPE_OF_DISCRETE_RANGE (DISCRETE_RANGE : TREE) return TREE is
    RESULT : TREE;
  begin
    case DISCRETE_RANGE.TY is
      when DN_RANGE =>
        RESULT := COPY_NODE (GET_BASE_STRUCT (D (SM_TYPE_SPEC, DISCRETE_RANGE)));
        if RESULT.TY in DN_ENUMERATION .. DN_INTEGER then
          D (SM_RANGE, RESULT, DISCRETE_RANGE);
          D (SM_DERIVED, RESULT, TREE_VOID);
          DB (SM_IS_ANONYMOUS, RESULT, True);
        else
          RESULT := TREE_VOID;
        end if;
        return RESULT;
      when DN_RANGE_ATTRIBUTE =>
        declare
          PREFIX          : TREE    := D (AS_NAME, DISCRETE_RANGE);
          PREFIX_SUBTYPE  : TREE;
          WHICH_SUBSCRIPT : Integer := 1;
          INDEX_LIST      : SEQ_TYPE;
          INDEX           : TREE;
        begin
          if D (AS_EXP, DISCRETE_RANGE) /= TREE_VOID then
            if GET_STATIC_VALUE (D (AS_EXP, DISCRETE_RANGE)) /= TREE_VOID then
              WHICH_SUBSCRIPT := DI (SM_VALUE, D (AS_EXP, DISCRETE_RANGE));
            else
              WHICH_SUBSCRIPT := -1;
            end if;
          end if;
          if PREFIX.TY = DN_SELECTED then
            PREFIX := D (AS_DESIGNATOR, PREFIX);
          end if;
          if PREFIX.TY = DN_USED_NAME_ID then
                                                -- IT'S A TYPE MARK
            PREFIX_SUBTYPE := D (SM_TYPE_SPEC, D (SM_DEFN, PREFIX));
          else
            PREFIX_SUBTYPE := D (SM_EXP_TYPE, PREFIX);
            if GET_BASE_STRUCT (PREFIX_SUBTYPE).TY = DN_ACCESS then
              PREFIX_SUBTYPE := D (SM_DESIG_TYPE, GET_SUBSTRUCT (PREFIX_SUBTYPE));
            end if;
          end if;
          PREFIX_SUBTYPE := GET_SUBSTRUCT (PREFIX_SUBTYPE);
          if PREFIX_SUBTYPE.TY = DN_CONSTRAINED_ARRAY then
            INDEX_LIST := LIST (D (SM_INDEX_SUBTYPE_S, PREFIX_SUBTYPE));
          elsif PREFIX_SUBTYPE.TY = DN_ARRAY then
            INDEX_LIST := LIST (D (SM_INDEX_S, PREFIX_SUBTYPE));
          else
            INDEX_LIST := (TREE_NIL, TREE_NIL);
          end if;
          loop
            if IS_EMPTY (INDEX_LIST) then
                                                        -- (ERROR ALREADY REPORTED)
              return TREE_VOID;
            end if;
            POP (INDEX_LIST, INDEX);
            WHICH_SUBSCRIPT := WHICH_SUBSCRIPT - 1;
            exit when WHICH_SUBSCRIPT = 0;
          end loop;
          if INDEX.TY = DN_INDEX then
            return D (SM_TYPE_SPEC, INDEX);
          else
            return INDEX;
          end if;
        end;
      when DN_DISCRETE_SUBTYPE =>
        declare
          SUBTYPE_INDICATION : constant TREE := D (AS_SUBTYPE_INDICATION, DISCRETE_RANGE);
          CONSTRAINT         : constant TREE := D (AS_CONSTRAINT, SUBTYPE_INDICATION);
          NAME_DEFN          : TREE;
        begin
          if CONSTRAINT.TY = DN_RANGE then
            return GET_SUBTYPE_OF_DISCRETE_RANGE (CONSTRAINT);
          else
            NAME_DEFN := GET_NAME_DEFN (D (AS_NAME, SUBTYPE_INDICATION));
            if NAME_DEFN /= TREE_VOID then
              return D (SM_TYPE_SPEC, NAME_DEFN);
            else
              return TREE_VOID;
            end if;
          end if;
        end;
      when others =>
        Put_Line ("!!INVALID DISCRETE RANGE");
        raise Program_Error;
    end case;
  end GET_SUBTYPE_OF_DISCRETE_RANGE;

  procedure WALK_COMP_LIST (COMP_LIST : TREE; H : H_TYPE) is
                -- WALK THE COMPONENT LIST (FIXED PART + VARIANT PART + PRAGMAS)
                -- ... IN A RECORD DECLARATION OR [RECURSIVELY] IN A VARIANT PART
                -- ... (CALLED FROM EVAL_TYPE_DEF FOR RECORD DECLARATION)

    DECL_S       : constant TREE := D (AS_DECL_S, COMP_LIST);
    VARIANT_PART : constant TREE := D (AS_VARIANT_PART, COMP_LIST);
    PRAGMA_S     : constant TREE := D (AS_PRAGMA_S, COMP_LIST);

  begin

                -- WALK THE FIXED PART
    WALK_ITEM_S (DECL_S, H);

                -- IF THERE IS A VARIANT PART
    if VARIANT_PART /= TREE_VOID then

      declare
        NAME      : TREE          := D (AS_NAME, VARIANT_PART);
        VARIANT_S : constant TREE := D (AS_VARIANT_S, VARIANT_PART);

        NAME_TYPE    : TREE;
        VARIANT_LIST : SEQ_TYPE := LIST (VARIANT_S);
        VARIANT      : TREE;
      begin
                                -- $$$$ NEED TO ALLOW DISCRIMINANT NAMES AT APPROPRIATE POINTS

                                -- EVALUATE THE DISCRIMINANT NAME
                                -- ... (SYNTAX REQUIRES SIMPLE NAME)
        NAME      := WALK_NAME (DN_DISCRIMINANT_ID, NAME);
        NAME_TYPE := GET_BASE_TYPE (NAME);

                                -- FOR EACH VARIANT OR PRAGMA
        while not IS_EMPTY (VARIANT_LIST) loop
          POP (VARIANT_LIST, VARIANT);

                                        -- IF IT IS A VARIANT
          if VARIANT.TY = DN_VARIANT then

                                                -- WALK THE CHOICE LIST
            WALK_DISCRETE_CHOICE_S (D (AS_CHOICE_S, VARIANT), NAME_TYPE);

                                                -- WALK THE VARIANT COMPONENT LIST
            WALK_COMP_LIST (D (AS_COMP_LIST, VARIANT), H);

                                                -- ELSE -- SINCE IT MUST BE A VARIANT_PRAGMA
          else

                                                -- WALK THE PRAGMA
            WALK (D (AS_PRAGMA, VARIANT), H);
          end if;
        end loop;
      end;
    end if;

                -- WALK THE PRAGMA PART
    WALK_ITEM_S (PRAGMA_S, H);
  end WALK_COMP_LIST;

   --|----------------------------------------------------------------------------------------------
end DEF_WALK;
