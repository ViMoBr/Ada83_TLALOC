separate (IDL.SEM_PHASE)
    --|----------------------------------------------------------------------------------------------
    --| EXPRESO
    --|----------------------------------------------------------------------------------------------
package body EXPRESO is
  use DEF_UTIL;
  use SEM_GLOB;
  use VIS_UTIL;
  use EXP_TYPE;
  use MAKE_NOD;
  use RED_SUBP;
  use REQ_UTIL;
  use DEF_WALK;
  use AGGRESO;
  use ATT_WALK;

  function WALK_DISCRMT_CONSTRAINT (RECORD_TYPE : TREE; GENERAL_ASSOC_S : TREE) return TREE;

  function RESOLVE_RANGE (EXP : TREE; TYPE_SPEC : TREE) return TREE;

  function GET_NAME_DEFN (NAME : TREE) return TREE is
  begin
    case NAME.TY is
      when DN_VOID =>
        return TREE_VOID;
      when DN_SELECTED =>
        return D (SM_DEFN, D (AS_DESIGNATOR, NAME));
      when CLASS_DESIGNATOR =>
        return D (SM_DEFN, NAME);
      when others =>
        Put_Line ("!! GET_NAME_DEFN: INVALID PARAMETER");
        raise Program_Error;
    end case;
  end GET_NAME_DEFN;

        -- $$$$ PROBABLY SHOULDN'T BE HERE
  function LENGTH (A : SEQ_TYPE) return Natural is
    COUNT : Natural  := 0;
    ATAIL : SEQ_TYPE := A;
  begin
    while not IS_EMPTY (ATAIL) loop
      COUNT := COUNT + 1;
      ATAIL := TAIL (ATAIL);
    end loop;
    return COUNT;
  end LENGTH;

  function APPROP_STRUCT (TYPE_SPEC : TREE) return TREE is
    TYPE_STRUCT : TREE := GET_BASE_STRUCT (TYPE_SPEC);
  begin
    if TYPE_STRUCT.TY = DN_ACCESS then
      TYPE_STRUCT := GET_BASE_STRUCT (D (SM_DESIG_TYPE, TYPE_STRUCT));
    end if;
    return TYPE_STRUCT;
  end APPROP_STRUCT;
--|#################################################################################################
--|
  function GET_STATIC_VALUE (EXP : TREE) return TREE is
  begin
    case EXP.TY is
      when CLASS_USED_OBJECT | CLASS_NAME_VAL | CLASS_EXP_VAL =>
        return D (SM_VALUE, EXP);
      when DN_CONSTANT_ID | DN_NUMBER_ID =>
        return GET_STATIC_VALUE (D (SM_INIT_EXP, EXP));
      when CLASS_ENUM_LITERAL =>
        return UARITH.U_VAL (DI (SM_POS, EXP));
      when others =>
        return TREE_VOID;
    end case;
  end GET_STATIC_VALUE;

        --========================================================================

  function RESOLVE_EXP (EXP : TREE; TYPE_SPEC : TREE) return TREE is
    EXP_KIND : constant NODE_NAME := EXP.TY;

  begin
                -- SHOULD BE SYNTACTICAL EXPRESSION OR VOID
    if EXP_KIND not in CLASS_EXP then
                        -- PRESUMABLY ANY ERROR MESSAGES HAVE BEEN GIVEN
      if EXP_KIND = DN_RANGE or EXP_KIND = DN_DISCRETE_SUBTYPE then
        return RESOLVE_DISCRETE_RANGE (EXP, TYPE_SPEC);
      else
        return EXP;
      end if;
    end if;

    case CLASS_EXP'(EXP_KIND) is

      when DN_USED_CHAR | DN_USED_OBJECT_ID =>
        declare
          DEFSET     : DEFSET_TYPE;
          DEFINTERP  : DEFINTERP_TYPE;
          DEF        : TREE;
          DEF_TYPE   : TREE;
          NEW_DEFSET : DEFSET_TYPE := EMPTY_DEFSET;

          DEFN : TREE := TREE_VOID;
        begin
          if TYPE_SPEC /= TREE_VOID then
            DEFSET := FETCH_DEFSET (EXP);
            while not IS_EMPTY (DEFSET) loop
              POP (DEFSET, DEFINTERP);
              DEF      := GET_DEF (DEFINTERP);
              DEF_TYPE := EXPRESSION_TYPE_OF_DEF (DEF);
              if DEF_TYPE = TYPE_SPEC or else (DEF_TYPE.TY = DN_ANY_INTEGER and then (TYPE_SPEC.TY = DN_INTEGER or else TYPE_SPEC.TY = DN_UNIVERSAL_INTEGER))
               or else (DEF_TYPE.TY = DN_ANY_REAL and then (TYPE_SPEC.TY = DN_FLOAT or else TYPE_SPEC.TY = DN_FIXED or else TYPE_SPEC.TY = DN_UNIVERSAL_REAL))
              then
                ADD_TO_DEFSET (NEW_DEFSET, DEFINTERP);
              end if;
            end loop;
            if IS_EMPTY (NEW_DEFSET) then
              ERROR (D (LX_SRCPOS, EXP), "**** NO DEFS IN RESOLVE");
            end if;
            REQUIRE_UNIQUE_DEF (EXP, NEW_DEFSET);
            DEFN := GET_THE_ID (NEW_DEFSET);
          end if;

          D (SM_DEFN, EXP, DEFN);
          if DEFN.TY = DN_FUNCTION_ID or else DEFN.TY = DN_GENERIC_ID then
                                                -- IT'S FUNCTION CALL WITH ALL DEFAULT ARGS
            declare
              NEW_EXP : TREE;
            begin
              NEW_EXP :=
               MAKE_FUNCTION_CALL
                (LX_SRCPOS => D (LX_SRCPOS, EXP), AS_NAME => MAKE_USED_NAME_ID_FROM_OBJECT (EXP), AS_GENERAL_ASSOC_S => MAKE_GENERAL_ASSOC_S (LIST => (TREE_NIL, TREE_NIL)), SM_EXP_TYPE => D (SM_TYPE_SPEC, GET_NAME_DEFN (D (AS_NAME, D (SM_SPEC, DEFN)))));
                                                        -- MAKE NORMALIZED_PARAM_S FOR DEFAULT PARAMS
              D (SM_NORMALIZED_PARAM_S, NEW_EXP, RESOLVE_SUBP_PARAMETERS (GET_DEF (HEAD (NEW_DEFSET)), D (AS_GENERAL_ASSOC_S, NEW_EXP)));
              return NEW_EXP;
            end;
          elsif DEFN.TY in CLASS_TYPE_NAME then
                                                -- (FOR NAME OF TASK TYPE INSIDE THE TASK BODY)
            D (SM_EXP_TYPE, EXP, D (SM_TYPE_SPEC, DEFN));
          elsif DEFN /= TREE_VOID then
            D (SM_EXP_TYPE, EXP, D (SM_OBJ_TYPE, DEFN));
            D (SM_VALUE, EXP, GET_STATIC_VALUE (DEFN));
          else
            D (SM_EXP_TYPE, EXP, TREE_VOID);
          end if;
        end;

      when DN_USED_OP =>
        Put_Line ("!! INVALID PARAMETER FOR RESOLVE_EXP");
        raise Program_Error;

      when DN_USED_NAME_ID =>
                                -- ALREADY RESOLVED
        null;

      when DN_ATTRIBUTE =>
        declare
          NEW_EXP : TREE;
        begin
          NEW_EXP := RESOLVE_ATTRIBUTE (EXP);
          if D (SM_EXP_TYPE, NEW_EXP).TY in CLASS_UNSPECIFIED_TYPE then
            D (SM_EXP_TYPE, NEW_EXP, TYPE_SPEC);
          end if;
          return NEW_EXP;
        end;

      when DN_SELECTED =>
        declare
          NAME              : TREE := D (AS_NAME, EXP);
          DESIGNATOR        : TREE := D (AS_DESIGNATOR, EXP);
          DESIGNATOR_REGION : TREE := TREE_VOID;

          NAME_TYPESET     : TYPESET_TYPE;
          NAME_TYPEINTERP  : TYPEINTERP_TYPE;
          NEW_NAME_TYPESET : TYPESET_TYPE := EMPTY_TYPESET;
        begin

                                        -- RESOLVE THE DESIGNATOR
          DESIGNATOR := RESOLVE_EXP (DESIGNATOR, TYPE_SPEC);
          D (AS_DESIGNATOR, EXP, DESIGNATOR);

                                        -- IF DESIGNATOR REPRESENTS AN EXPRESSION
          if DESIGNATOR.TY in CLASS_USED_OBJECT then

                                                -- COPY VALUE AND SUBTYPE
            D (SM_VALUE, EXP, GET_STATIC_VALUE (DESIGNATOR));
            D (SM_EXP_TYPE, EXP, D (SM_EXP_TYPE, DESIGNATOR));

                                                -- IF PREFIX CAN BE EXPRESSION
            if NAME.TY not in CLASS_USED_NAME then

                                                        -- GET SAVED TYPESET FOR NAME
              NAME_TYPESET := FETCH_TYPESET (NAME);

                                                        -- GET POSSIBLE TYPES OF PREFIX
              if D (SM_DEFN, DESIGNATOR) /= TREE_VOID then
                DESIGNATOR_REGION := D (XD_SOURCE_NAME, D (XD_REGION_DEF, GET_DEF_FOR_ID (D (SM_DEFN, DESIGNATOR))));
              end if;
              while not IS_EMPTY (NAME_TYPESET) loop
                POP (NAME_TYPESET, NAME_TYPEINTERP);
                if D (XD_SOURCE_NAME, APPROP_STRUCT (GET_TYPE (NAME_TYPEINTERP))) = DESIGNATOR_REGION then
                  ADD_TO_TYPESET (NEW_NAME_TYPESET, NAME_TYPEINTERP);
                end if;
              end loop;

                                                        -- REQUIRE A UNIQUE TYPE
              REQUIRE_UNIQUE_TYPE (NAME, NEW_NAME_TYPESET);

                                                        -- RESOLVE THE NAME
              NAME := RESOLVE_EXP (NAME, NEW_NAME_TYPESET);
              D (AS_NAME, EXP, NAME);
            end if;

                                                -- ELSE IF DESIGNATOR IS A FUNCTION CALL
          elsif DESIGNATOR.TY = DN_FUNCTION_CALL then

                                                -- REPLACE:
                                                --          SELECTED
                                                --            AS_NAME: <PREFIX_NAME>
                                                --            AS_DESIGNATOR: FUNCTION_CALL
                                                --              AS_NAME: <FUNCTION_NAME>
                                                --              ...
                                                -- BY:
                                                --          FUNCTION_CALL
                                                --            AS_NAME: SELECTED
                                                --              AS_NAME: <PREFIX_NAME>
                                                --              AS_DESIGNATOR: <FUNCTION_NAME>
                                                --            ...
            D (AS_DESIGNATOR, EXP, D (AS_NAME, DESIGNATOR));
            D (SM_EXP_TYPE, EXP, TREE_VOID);
            D (AS_NAME, DESIGNATOR, EXP);
            D (LX_SRCPOS, DESIGNATOR, D (LX_SRCPOS, NAME));
            return DESIGNATOR;
          end if;
        end;

      when DN_FUNCTION_CALL =>
        return RESOLVE_FUNCTION_CALL (EXP, TYPE_SPEC);

      when DN_INDEXED =>
        Put_Line ("!! RESOLVE_EXP: INVALID NODE");
        raise Program_Error;

      when DN_SLICE =>
        Put_Line ("!! RESOLVE_EXP: INVALID NODE");
        raise Program_Error;

      when DN_ALL =>
        declare
          NAME             : TREE         := D (AS_NAME, EXP);
          NAME_TYPESET     : TYPESET_TYPE := FETCH_TYPESET (NAME);
          NAME_TYPEINTERP  : TYPEINTERP_TYPE;
          NAME_STRUCT      : TREE;
          NEW_NAME_TYPESET : TYPESET_TYPE := EMPTY_TYPESET;
        begin

                                        -- GET LIST OF NAME TYPES WITH REQUIRED DESIG TYPE
                                        -- ... (FIND AT LEAST ONE UNLESS TYPE_SPEC IS VOID)
          while not IS_EMPTY (NAME_TYPESET) loop
            POP (NAME_TYPESET, NAME_TYPEINTERP);
            NAME_STRUCT := GET_BASE_STRUCT (GET_TYPE (NAME_TYPEINTERP));
            if GET_BASE_TYPE (D (SM_DESIG_TYPE, NAME_STRUCT)) = TYPE_SPEC then
              ADD_TO_TYPESET (NEW_NAME_TYPESET, NAME_TYPEINTERP);
            end if;
          end loop;

                                        -- RESOLVE THE NAME
          REQUIRE_UNIQUE_TYPE (NAME, NEW_NAME_TYPESET);
          NAME := RESOLVE_EXP (NAME, NEW_NAME_TYPESET);
          D (AS_NAME, EXP, NAME);

                                        -- EXPRESSION TYPE IS DESIGNATED SUBTYPE OF NAME TYPE
          if not IS_EMPTY (NEW_NAME_TYPESET) then
            D (SM_EXP_TYPE, EXP, D (SM_DESIG_TYPE, GET_BASE_STRUCT (GET_THE_TYPE (NEW_NAME_TYPESET))));
          else
            D (SM_EXP_TYPE, EXP, TYPE_SPEC);
          end if;
        end;

      when DN_SHORT_CIRCUIT =>
        declare
          EXP1 : TREE := D (AS_EXP1, EXP);
          EXP2 : TREE := D (AS_EXP2, EXP);
        begin

                                        -- RESOLVE THE TWO EXPRESSIONS
          EXP1 := RESOLVE_EXP (EXP1, TYPE_SPEC);
          D (AS_EXP1, EXP, EXP1);
          EXP2 := RESOLVE_EXP (EXP2, TYPE_SPEC);
          D (AS_EXP2, EXP, EXP2);

                                        -- STORE THE RESULT TYPE
          D (SM_EXP_TYPE, EXP, TYPE_SPEC);
        end;

      when DN_NUMERIC_LITERAL =>					-- VALUE ALREADY KNOWN

          D( SM_EXP_TYPE, EXP, TYPE_SPEC );				-- STORE TYPE WHICH IS RESULT OF ANY IMPLICIT CONVERSION

      when DN_NULL_ACCESS =>

          D( SM_EXP_TYPE, EXP, TYPE_SPEC );				-- STORE THE RESULT TYPE

      when DN_RANGE_MEMBERSHIP =>
        declare
          EXP_NODE       : TREE := D (AS_EXP, EXP);
          RANGE_NODE     : TREE := D (AS_RANGE, EXP);
          EXP_TYPESET    : TYPESET_TYPE;
          RANGE_TYPESET  : TYPESET_TYPE;
          TYPESET        : TYPESET_TYPE;
          TYPE_MARK_TYPE : TREE;
        begin
          EVAL_EXP_TYPES (EXP_NODE, EXP_TYPESET);
          if RANGE_NODE.TY = DN_RANGE or RANGE_NODE.TY = DN_ATTRIBUTE or (RANGE_NODE.TY = DN_FUNCTION_CALL and then D (AS_NAME, RANGE_NODE).TY = DN_ATTRIBUTE) then
            EVAL_RANGE (RANGE_NODE, RANGE_TYPESET);
            REQUIRE_SAME_TYPES (EXP_NODE, EXP_TYPESET, RANGE_NODE, RANGE_TYPESET, TYPESET);
            REQUIRE_UNIQUE_TYPE (RANGE_NODE, TYPESET);
            EXP_NODE := RESOLVE_EXP (EXP_NODE, TYPESET);
            D (AS_EXP, EXP, EXP_NODE);
            RANGE_NODE := RESOLVE_RANGE (RANGE_NODE, GET_THE_TYPE (TYPESET));
            D (AS_RANGE, EXP, RANGE_NODE);
            D (SM_EXP_TYPE, EXP, PREDEFINED_BOOLEAN);
          else
            TYPE_MARK_TYPE := EVAL_TYPE_MARK (RANGE_NODE);
            RANGE_NODE     := RESOLVE_TYPE_MARK (RANGE_NODE);
            REQUIRE_TYPE (TYPE_MARK_TYPE, EXP_NODE, EXP_TYPESET);
            EXP_NODE := RESOLVE_EXP (EXP_NODE, EXP_TYPESET);
            return MAKE_TYPE_MEMBERSHIP (LX_SRCPOS => D (LX_SRCPOS, EXP), AS_EXP => EXP_NODE, AS_NAME => RANGE_NODE, AS_MEMBERSHIP_OP => D (AS_MEMBERSHIP_OP, EXP), SM_EXP_TYPE => PREDEFINED_BOOLEAN);
          end if;
        end;

      when DN_TYPE_MEMBERSHIP | DN_CONVERSION =>
        Put_Line ("RESOLVE_EXP: INVALID NODE");
        raise Program_Error;

      when DN_QUALIFIED =>
        declare
          EXP_NODE : TREE := D (AS_EXP, EXP);
          NAME     : TREE := D (AS_NAME, EXP);

          EXP_TYPESET  : TYPESET_TYPE;
          NAME_DEFN    : TREE;
          SUBTYPE_SPEC : TREE;
          VALUE        : TREE := D (SM_VALUE, EXP);
        begin
          NAME := RESOLVE_TYPE_MARK (NAME);
          D (AS_NAME, EXP, NAME);
          NAME_DEFN := GET_NAME_DEFN (NAME);
          if NAME_DEFN /= TREE_VOID then
            SUBTYPE_SPEC := D (SM_TYPE_SPEC, NAME_DEFN);
          else
            SUBTYPE_SPEC := TREE_VOID;
          end if;

          EVAL_EXP_TYPES (EXP_NODE, EXP_TYPESET);
          REQUIRE_TYPE (GET_BASE_TYPE (SUBTYPE_SPEC), EXP_NODE, EXP_TYPESET);
          if not IS_EMPTY (EXP_TYPESET) then
            EXP_NODE := RESOLVE_EXP_OR_AGGREGATE (EXP_NODE, SUBTYPE_SPEC, NAMED_OTHERS_OK => True);
          else
            EXP_NODE := RESOLVE_EXP (EXP_NODE, TREE_VOID);
          end if;
          D (AS_EXP, EXP, EXP_NODE);

          D (SM_EXP_TYPE, EXP, SUBTYPE_SPEC);
          D (SM_VALUE, EXP, GET_STATIC_VALUE (EXP_NODE));
        end;

      when DN_PARENTHESIZED =>
        declare
          EXP_NODE : TREE := D (AS_EXP, EXP);
        begin
          EXP_NODE := RESOLVE_EXP (EXP_NODE, TYPE_SPEC);
          D (AS_EXP, EXP, EXP_NODE);
          D (SM_EXP_TYPE, EXP, D (SM_EXP_TYPE, EXP_NODE));
          D (SM_VALUE, EXP, GET_STATIC_VALUE (EXP_NODE));
        end;

      when DN_AGGREGATE =>
        RESOLVE_AGGREGATE (EXP, TYPE_SPEC);

      when DN_STRING_LITERAL =>
        RESOLVE_STRING (EXP, TYPE_SPEC);

      when DN_QUALIFIED_ALLOCATOR =>
        declare
          QUALIFIED : TREE := D (AS_QUALIFIED, EXP);
        begin
                                        -- (NOTE: REQUIRED TYPE IGNORED IN RESOLVE_EXP)
          QUALIFIED := RESOLVE_EXP (QUALIFIED, TREE_VOID);
          D (SM_EXP_TYPE, EXP, TYPE_SPEC);
        end;

      when DN_SUBTYPE_ALLOCATOR =>
        declare
          SUBTYPE_INDICATION : TREE := D (AS_SUBTYPE_INDICATION, EXP);
          EXP_TYPE           : TREE := D (SM_EXP_TYPE, EXP);
          DESIG_TYPE         : TREE := D (SM_DESIG_TYPE, EXP);
          SUBTYPE_SPEC       : TREE;
        begin
          RESOLVE_SUBTYPE_INDICATION (SUBTYPE_INDICATION, SUBTYPE_SPEC);
          D (AS_SUBTYPE_INDICATION, EXP, SUBTYPE_INDICATION);
          D (SM_EXP_TYPE, EXP, TYPE_SPEC);
          D (SM_DESIG_TYPE, EXP, SUBTYPE_SPEC);
        end;

    end case;

                --$$$$$ NEED TO HAVE A TEMPORARY FOR EXP
    return EXP;
  end RESOLVE_EXP;

        ------------------------------------------------------------------------

  function RESOLVE_RANGE (EXP : TREE; TYPE_SPEC : TREE) return TREE is
  begin
                -- IF IT IS A RANGE
    if EXP.TY = DN_RANGE then

                        -- SAVE THE RANGE TYPE AND RESOLVE THE BOUNDS
      D (SM_TYPE_SPEC, EXP, TYPE_SPEC);
      D (AS_EXP1, EXP, RESOLVE_EXP (D (AS_EXP1, EXP), TYPE_SPEC));
      D (AS_EXP2, EXP, RESOLVE_EXP (D (AS_EXP2, EXP), TYPE_SPEC));
      return EXP;

                        -- ELSE IF IT IS A RANGE ATTRIBUTE
    elsif EXP.TY = DN_ATTRIBUTE or else (EXP.TY = DN_FUNCTION_CALL and then D (AS_NAME, EXP).TY = DN_ATTRIBUTE) then

                        -- RESOLVE THE ATTRIBUTE
      return RESOLVE_ATTRIBUTE (EXP);

    else
      Put_Line ("!! RESOLVE_RANGE: NOT A RANGE");
      raise Program_Error;
    end if;
  end RESOLVE_RANGE;

  function RESOLVE_DISCRETE_RANGE (EXP : TREE; TYPE_SPEC : TREE) return TREE is
  begin
                -- IF IT IS A RANGE OR RANGE ATTRIBUTE
    if EXP.TY = DN_RANGE or EXP.TY = DN_ATTRIBUTE or EXP.TY = DN_FUNCTION_CALL then

                        -- RESOLVE THE RANGE
      return RESOLVE_RANGE (EXP, TYPE_SPEC);

                        -- ELSE IF IT IS A DISCRETE_SUBTYPE (INTERMEDIATE NODE)
    elsif EXP.TY = DN_DISCRETE_SUBTYPE then

                        -- RESOLVE THE SUBTYPE INDICATION
      declare
        SUBTYPE_INDICATION : TREE := D (AS_SUBTYPE_INDICATION, EXP);
        THE_SUBTYPE        : TREE;
      begin
        RESOLVE_SUBTYPE_INDICATION (SUBTYPE_INDICATION, THE_SUBTYPE);
      end;
      return EXP;

                        -- ELSE IF IT IS SUBTYPE INDICATION
    elsif EXP.TY = DN_SUBTYPE_INDICATION then

                        -- MAKE DISCRETE SUBTYPE NODE AND RESOLVE
      return RESOLVE_DISCRETE_RANGE (MAKE_DISCRETE_SUBTYPE (LX_SRCPOS => D (LX_SRCPOS, EXP), AS_SUBTYPE_INDICATION => EXP), TYPE_SPEC);

                        -- ELSE -- SINCE IT MUST BE A TYPE MARK
    else

                        -- MAKE SUBTYPE INDICATION AND RESOLVE
      return RESOLVE_DISCRETE_RANGE (MAKE_SUBTYPE_INDICATION (LX_SRCPOS => D (LX_SRCPOS, EXP), AS_NAME => EXP, AS_CONSTRAINT => TREE_VOID), TYPE_SPEC);

    end if;

  end RESOLVE_DISCRETE_RANGE;

        ------------------------------------------------------------------------

  function RESOLVE_TYPE_MARK (EXP : TREE) return TREE is
  begin
    if EXP.TY = DN_SUBTYPE_INDICATION then
      return RESOLVE_TYPE_MARK (D (AS_NAME, EXP));
                        -- NOTE ERROR ALREADY GIVEN IF NON-VOID CONSTRAINT
    end if;

    if EXP.TY = DN_SELECTED then
      D (AS_DESIGNATOR, EXP, RESOLVE_TYPE_MARK (D (AS_DESIGNATOR, EXP)));
      D (SM_EXP_TYPE, EXP, TREE_VOID);
      return EXP;

    elsif EXP.TY = DN_USED_OBJECT_ID then
                        -- $$$$ SOMETIMES STILL A DEF? WHY?
      if D (SM_DEFN, EXP).TY = DN_DEF then
        D (SM_DEFN, EXP, D (XD_SOURCE_NAME, D (SM_DEFN, EXP)));
      end if;
      return MAKE_USED_NAME_ID_FROM_OBJECT (EXP);

    else
      return EXP;
    end if;
  end RESOLVE_TYPE_MARK;

        ------------------------------------------------------------------------

  procedure WALK_RANGE (BASE_TYPE : TREE; RANGE_NODE : TREE) is
    EXP1                 : TREE := D (AS_EXP1, RANGE_NODE);
    EXP2                 : TREE := D (AS_EXP2, RANGE_NODE);
    TYPESET_1, TYPESET_2 : TYPESET_TYPE;
  begin
    EVAL_EXP_TYPES (EXP1, TYPESET_1);
    EVAL_EXP_TYPES (EXP2, TYPESET_2);
    REQUIRE_TYPE (BASE_TYPE, EXP1, TYPESET_1);
    REQUIRE_TYPE (BASE_TYPE, EXP2, TYPESET_2);
    EXP1 := RESOLVE_EXP (EXP1, TYPESET_1);
    D (AS_EXP1, RANGE_NODE, EXP1);
    EXP2 := RESOLVE_EXP (EXP2, TYPESET_2);
    D (AS_EXP2, RANGE_NODE, EXP2);
    D (SM_TYPE_SPEC, RANGE_NODE, BASE_TYPE);
  end WALK_RANGE;

  procedure RESOLVE_SUBTYPE_INDICATION (EXP : in out TREE; SUBTYPE_SPEC : out TREE) is
    NAME          : TREE;
    NAME_DEFN     : TREE;
    CONSTRAINT    : TREE;
    TYPE_STRUCT   : TREE;
    DESIG_STRUCT  : TREE;
    NEW_TYPE_SPEC : TREE := TREE_VOID;
  begin
    if EXP.TY /= DN_SUBTYPE_INDICATION then
      if EXP.TY = DN_FUNCTION_CALL then
        Put_Line ("!! SUBTYPE_IND IS FUNCTION CALL $$$$$$");
        raise Program_Error;
      end if;

                        -- (MUST BE A TYPE MARK)
      EXP := MAKE_SUBTYPE_INDICATION (AS_NAME => EXP, AS_CONSTRAINT => TREE_VOID, LX_SRCPOS => D (LX_SRCPOS, EXP));
    end if;

    NAME := D (AS_NAME, EXP);
    NAME := RESOLVE_TYPE_MARK (NAME);
    D (AS_NAME, EXP, NAME);
    NAME_DEFN := GET_NAME_DEFN (NAME);
    if NAME_DEFN /= TREE_VOID then
      NEW_TYPE_SPEC := D (SM_TYPE_SPEC, NAME_DEFN);
    end if;
    TYPE_STRUCT := GET_BASE_STRUCT (NEW_TYPE_SPEC);
    if NEW_TYPE_SPEC.TY in CLASS_PRIVATE_SPEC
                                --AND THEN KIND(TYPE_STRUCT) NOT IN CLASS_PRIVATE_SPEC THEN
      and then TYPE_STRUCT /= D (SM_TYPE_SPEC, D (XD_SOURCE_NAME, TYPE_STRUCT)) then
      NEW_TYPE_SPEC := D (SM_TYPE_SPEC, NEW_TYPE_SPEC);
    end if;
    DESIG_STRUCT := TYPE_STRUCT;
    if DESIG_STRUCT.TY = DN_ACCESS then
      DESIG_STRUCT := GET_BASE_STRUCT (D (SM_DESIG_TYPE, DESIG_STRUCT));
    end if;

    CONSTRAINT := D (AS_CONSTRAINT, EXP);
    if CONSTRAINT.TY = DN_ATTRIBUTE or else (CONSTRAINT.TY = DN_FUNCTION_CALL and then D (AS_NAME, CONSTRAINT).TY = DN_ATTRIBUTE) then
      declare
        TYPESET : TYPESET_TYPE;
        IS_TYPE : Boolean;
      begin
        EVAL_ATTRIBUTE (CONSTRAINT, TYPESET, IS_TYPE);
        CONSTRAINT := RESOLVE_ATTRIBUTE (CONSTRAINT);
        if IS_EMPTY (TYPESET) and not IS_TYPE then
          CONSTRAINT := TREE_VOID;
        end if;
        D (AS_CONSTRAINT, EXP, CONSTRAINT);
      end;
    end if;
    case CONSTRAINT.TY is
      when DN_VOID =>
        null;
      when CLASS_RANGE =>
        if TYPE_STRUCT.TY in CLASS_SCALAR then
          if CONSTRAINT.TY = DN_RANGE then
            WALK_RANGE (GET_BASE_TYPE (TYPE_STRUCT), CONSTRAINT);
          end if;
                                        -- $$$$ IS THIS RIGHT FOR A RANGE ATTRIBUTE?
          if NEW_TYPE_SPEC.TY in CLASS_PRIVATE_SPEC then
            NEW_TYPE_SPEC := D (SM_TYPE_SPEC, NEW_TYPE_SPEC);
          elsif NEW_TYPE_SPEC.TY = DN_INCOMPLETE then
            NEW_TYPE_SPEC := D (XD_FULL_TYPE_SPEC, NEW_TYPE_SPEC);
          end if;
          D (SM_TYPE_SPEC, CONSTRAINT, NEW_TYPE_SPEC);

          NEW_TYPE_SPEC := COPY_NODE (TYPE_STRUCT);
          D (SM_RANGE, NEW_TYPE_SPEC, CONSTRAINT);
          D (SM_DERIVED, NEW_TYPE_SPEC, TREE_VOID);
        else
          ERROR (D (LX_SRCPOS, CONSTRAINT), "RANGE CONSTRAINT NOT ALLOWED");
        end if;
      when DN_FIXED_CONSTRAINT =>
        if TYPE_STRUCT.TY = DN_FIXED then
          declare
            RANGE_NODE : TREE := D (AS_RANGE, CONSTRAINT);
            EXP        : TREE := D (AS_EXP, CONSTRAINT);
            TYPESET    : TYPESET_TYPE;
            ACCURACY   : TREE;
          begin
            D (SM_TYPE_SPEC, CONSTRAINT, GET_BASE_TYPE (NEW_TYPE_SPEC));
            NEW_TYPE_SPEC := COPY_NODE (NEW_TYPE_SPEC);
            D (SM_DERIVED, NEW_TYPE_SPEC, TREE_VOID);
            EVAL_EXP_TYPES (EXP, TYPESET);
            REQUIRE_REAL_TYPE (EXP, TYPESET);
            REQUIRE_UNIQUE_TYPE (EXP, TYPESET);
            EXP := RESOLVE_EXP (EXP, TYPESET);
            D (AS_EXP, CONSTRAINT, EXP);
            ACCURACY := GET_STATIC_VALUE (EXP);
            if ACCURACY = TREE_VOID then
              ERROR (D (LX_SRCPOS, EXP), "STATIC DELTA REQUIRED");
            else
              D (SM_ACCURACY, NEW_TYPE_SPEC, ACCURACY);
            end if;

            if D (AS_RANGE, CONSTRAINT) /= TREE_VOID then
              WALK_RANGE (GET_BASE_TYPE (TYPE_STRUCT), RANGE_NODE);
              D (SM_RANGE, NEW_TYPE_SPEC, RANGE_NODE);
            end if;
          end;
        else
          ERROR (D (LX_SRCPOS, CONSTRAINT), "FIXED CONSTRAINT NOT ALLOWED");
        end if;

      when DN_FLOAT_CONSTRAINT =>
        if TYPE_STRUCT.TY = DN_FLOAT then
          declare
            RANGE_NODE : TREE := D (AS_RANGE, CONSTRAINT);
            EXP        : TREE := D (AS_EXP, CONSTRAINT);
            TYPESET    : TYPESET_TYPE;
            ACCURACY   : TREE;
          begin
            D (SM_TYPE_SPEC, CONSTRAINT, GET_BASE_TYPE (NEW_TYPE_SPEC));
            NEW_TYPE_SPEC := COPY_NODE (NEW_TYPE_SPEC);
            D (SM_DERIVED, NEW_TYPE_SPEC, TREE_VOID);
            EVAL_EXP_TYPES (EXP, TYPESET);
            REQUIRE_INTEGER_TYPE (EXP, TYPESET);
            REQUIRE_UNIQUE_TYPE (EXP, TYPESET);
            EXP := RESOLVE_EXP (EXP, TYPESET);
            D (AS_EXP, CONSTRAINT, EXP);
            ACCURACY := GET_STATIC_VALUE (EXP);
            if ACCURACY = TREE_VOID then
              ERROR (D (LX_SRCPOS, EXP), "STATIC DIGITS REQUIRED");
            else
              D (SM_ACCURACY, NEW_TYPE_SPEC, ACCURACY);
            end if;

            if D (AS_RANGE, CONSTRAINT) /= TREE_VOID then
              WALK_RANGE (GET_BASE_TYPE (TYPE_STRUCT), RANGE_NODE);
              D (SM_RANGE, NEW_TYPE_SPEC, RANGE_NODE);
            end if;
          end;
        else
          ERROR (D (LX_SRCPOS, CONSTRAINT), "FLOAT CONSTRAINT NOT ALLOWED");
                                        --    D(SM_DERIVED, NEW_TYPE_SPEC, CONST_VOID);
        end if;

      when DN_GENERAL_ASSOC_S =>

                                -- FOR A RECORD OR PRIVATE TYPE (MUST BE DISCRIMINANT CONSTRAINT)
        if DESIG_STRUCT.TY = DN_RECORD or else DESIG_STRUCT.TY in CLASS_PRIVATE_SPEC or else DESIG_STRUCT.TY = DN_INCOMPLETE then

          NEW_TYPE_SPEC := MAKE_CONSTRAINED_RECORD (SM_NORMALIZED_DSCRMT_S => WALK_DISCRMT_CONSTRAINT (DESIG_STRUCT, CONSTRAINT),
		XD_SOURCE_NAME => D (XD_SOURCE_NAME, DESIG_STRUCT), SM_BASE_TYPE => GET_BASE_STRUCT (DESIG_STRUCT));

          CONSTRAINT := MAKE_DSCRMT_CONSTRAINT (LX_SRCPOS => D (LX_SRCPOS, CONSTRAINT), AS_GENERAL_ASSOC_S => CONSTRAINT);
          D (AS_CONSTRAINT, EXP, CONSTRAINT);

                                        -- FOR AN ARRAY TYPE (MUST BE INDEX CONSTRAINT)
        elsif DESIG_STRUCT.TY = DN_ARRAY then
          declare
            DISCRETE_RANGE_LIST : SEQ_TYPE := LIST (CONSTRAINT);
            DISCRETE_RANGE      : TREE;
            INDEX_LIST          : SEQ_TYPE := LIST (D (SM_INDEX_S, DESIG_STRUCT));
            INDEX               : TREE;
            TYPESET             : TYPESET_TYPE;
            NEW_RANGE_LIST      : SEQ_TYPE := (TREE_NIL, TREE_NIL);
            SCALAR_LIST         : SEQ_TYPE := (TREE_NIL, TREE_NIL);
          begin
                                                -- FOR EACH MATCHING INDEX
            while not IS_EMPTY (INDEX_LIST) and not IS_EMPTY (DISCRETE_RANGE_LIST) loop
              POP (INDEX_LIST, INDEX);
              POP (DISCRETE_RANGE_LIST, DISCRETE_RANGE);

                                                        -- EVAL AND RESOLVE THE DISCRETE RANGE
              EVAL_DISCRETE_RANGE (DISCRETE_RANGE, TYPESET);
              REQUIRE_TYPE (GET_BASE_TYPE (D (SM_TYPE_SPEC, INDEX)), DISCRETE_RANGE, TYPESET);
              DISCRETE_RANGE := RESOLVE_DISCRETE_RANGE (DISCRETE_RANGE, GET_THE_TYPE (TYPESET));
              NEW_RANGE_LIST := APPEND (NEW_RANGE_LIST, DISCRETE_RANGE);

                                                        -- CONSTRUCT SUBTYPE FOR THIS INDEX AND ADD TO SCALAR_S
              SCALAR_LIST := APPEND (SCALAR_LIST, GET_SUBTYPE_OF_DISCRETE_RANGE (DISCRETE_RANGE));
            end loop;

                                                -- CHECK FOR DIMENSION MISMATCH
            if not IS_EMPTY (INDEX_LIST) then
              ERROR (D (LX_SRCPOS, CONSTRAINT), "TOO FEW ELEMENTS IN INDEX CONSTRAINT");
            elsif not IS_EMPTY (DISCRETE_RANGE_LIST) then
              ERROR (D (LX_SRCPOS, HEAD (DISCRETE_RANGE_LIST)), "TOO MANY ELEMENTS IN INDEX CONSTRAINT");
            end if;

                                                -- CONSTRUCT INDEX CONSTRAINT WITH RESOLVED EXPRESSIONS
            CONSTRAINT := MAKE_INDEX_CONSTRAINT (AS_DISCRETE_RANGE_S => MAKE_DISCRETE_RANGE_S (LIST => NEW_RANGE_LIST, LX_SRCPOS => D (LX_SRCPOS, CONSTRAINT)), LX_SRCPOS => D (LX_SRCPOS, CONSTRAINT));
            D (AS_CONSTRAINT, EXP, CONSTRAINT);

                                                -- MAKE NEW CONSTRAINED ARRAY SUBTYPE
            NEW_TYPE_SPEC := MAKE_CONSTRAINED_ARRAY (SM_INDEX_SUBTYPE_S => MAKE_SCALAR_S (LIST => SCALAR_LIST), SM_BASE_TYPE => D (SM_BASE_TYPE, DESIG_STRUCT), XD_SOURCE_NAME => D (XD_SOURCE_NAME, DESIG_STRUCT));
          end;

        else
          ERROR (D (LX_SRCPOS, CONSTRAINT), "INDEX OR DISCRIMINANT CONSTRAINT NOT ALLOWED");
        end if;

                                -- IF TYPE MARK WAS AN ACCESS TYPE
        if TYPE_STRUCT.TY = DN_ACCESS then

                                        -- MAKE CONSTRAINED ACCESS SUBTYPE
          NEW_TYPE_SPEC := MAKE_CONSTRAINED_ACCESS (SM_DESIG_TYPE => NEW_TYPE_SPEC, SM_BASE_TYPE => GET_BASE_STRUCT (TYPE_STRUCT), XD_SOURCE_NAME => D (XD_SOURCE_NAME, TYPE_STRUCT));
        end if;

      when others =>
        ERROR (D (LX_SRCPOS, CONSTRAINT), "NOT VALID AS A CONSTRAINT");
    end case;

    SUBTYPE_SPEC := NEW_TYPE_SPEC;
  end RESOLVE_SUBTYPE_INDICATION;

        ------------------------------------------------------------------------

  function RESOLVE_EXP (EXP : TREE; TYPESET : TYPESET_TYPE) return TREE is
    TEMP_TYPESET : TYPESET_TYPE := TYPESET;
  begin
    REQUIRE_UNIQUE_TYPE (EXP, TEMP_TYPESET);
    return RESOLVE_EXP (EXP, GET_THE_TYPE (TEMP_TYPESET));
  end RESOLVE_EXP;

        ------------------------------------------------------------------------

  function RESOLVE_NAME (NAME : TREE; DEFN : TREE) return TREE is
  begin
    if NAME.TY = DN_SELECTED then
      declare
        DESIGNATOR        : TREE         := D (AS_DESIGNATOR, NAME);
        PREFIX            : TREE;
        PREFIX_DEFSET     : DEFSET_TYPE;
        PREFIX_DEFINTERP  : DEFINTERP_TYPE;
        PREFIX_TYPESET    : TYPESET_TYPE;
        PREFIX_TYPEINTERP : TYPEINTERP_TYPE;
        PREFIX_TYPE       : TREE;
        NEW_TYPESET       : TYPESET_TYPE := EMPTY_TYPESET;
      begin
        DESIGNATOR := RESOLVE_NAME (DESIGNATOR, DEFN);
        D (AS_DESIGNATOR, NAME, DESIGNATOR);
        if DESIGNATOR.TY = DN_USED_OBJECT_ID or else DESIGNATOR.TY = DN_USED_CHAR then
          D (SM_EXP_TYPE, NAME, D (SM_EXP_TYPE, DESIGNATOR));
        else
          D (SM_EXP_TYPE, NAME, TREE_VOID);
        end if;

        PREFIX := D (AS_NAME, NAME);

                                -- IF THE PREFIX CAN BE AN EXPRESSION
                                -- (OTHERWISE IT IS ALREADY RESOLVED AS A USED NAME)

        if PREFIX.TY in CLASS_USED_OBJECT or else (PREFIX.TY = DN_SELECTED and then D (AS_DESIGNATOR, PREFIX).TY in CLASS_USED_OBJECT) or else (PREFIX.TY /= DN_SELECTED and then PREFIX.TY in CLASS_NAME_EXP) then

                                        -- GET THE TYPE OF THE PREFIX EXPRESSION
          if PREFIX.TY = DN_SELECTED or else PREFIX.TY in CLASS_DESIGNATOR then
                                                -- IT'S AN ID OR SELECTED ID, LOOK AT NAMES
            if DEFN /= TREE_VOID then
              PREFIX_DEFSET := FETCH_DEFSET (PREFIX);
            else
                                                        -- 8/9/90 AVOID CRASH FOR UNDEFINED DESIGNATOR
              PREFIX_DEFSET := EMPTY_DEFSET;
            end if;
            PREFIX_TYPESET := EMPTY_TYPESET;
            while not IS_EMPTY (PREFIX_DEFSET) loop
              POP (PREFIX_DEFSET, PREFIX_DEFINTERP);
              PREFIX_TYPE := GET_BASE_TYPE (D (XD_SOURCE_NAME, GET_DEF (PREFIX_DEFINTERP)));
              ADD_TO_TYPESET (PREFIX_TYPESET, PREFIX_TYPE, GET_EXTRAINFO (PREFIX_DEFINTERP));
            end loop;
          else
                                                -- IT'S A COMPLEX EXPRESSION, GET SAVED TYPESET
            PREFIX_TYPESET := FETCH_TYPESET (PREFIX);
          end if;

                                        -- SCAN TYPESET TO REPLACE ACCESSES WITH DESIGNATED TYPES
          while not IS_EMPTY (PREFIX_TYPESET) loop
            POP (PREFIX_TYPESET, PREFIX_TYPEINTERP);
            PREFIX_TYPE := GET_BASE_STRUCT (GET_TYPE (PREFIX_TYPEINTERP));
            if PREFIX_TYPE.TY = DN_ACCESS then
              PREFIX_TYPE := GET_BASE_STRUCT (D (SM_DESIG_TYPE, PREFIX_TYPE));
            end if;
            if DEFN /= TREE_VOID and then D (XD_REGION, DEFN) = D (XD_SOURCE_NAME, PREFIX_TYPE) then
              ADD_TO_TYPESET (NEW_TYPESET, PREFIX_TYPEINTERP);
            end if;
          end loop;
          if IS_EMPTY (NEW_TYPESET) and then DEFN /= TREE_VOID then
            ERROR (D (LX_SRCPOS, NAME), "***** NO DEFS FOR PREFIX OF SELECTED");
          end if;
          D (AS_NAME, NAME, RESOLVE_EXP (D (AS_NAME, NAME), NEW_TYPESET));
        end if;
        return NAME;
      end;
    elsif NAME.TY in CLASS_DESIGNATOR then
      D (SM_DEFN, NAME, DEFN);

      if NAME.TY = DN_USED_OBJECT_ID then
        if DEFN.TY in CLASS_OBJECT_NAME then
          D (SM_EXP_TYPE, NAME, D (SM_OBJ_TYPE, DEFN));
          return NAME;
        else
          return MAKE_USED_NAME_ID_FROM_OBJECT (NAME);
        end if;
      else
        return NAME;
      end if;

    else
      return NAME;
    end if;
  end RESOLVE_NAME;

        ------------------------------------------------------------------------

  function WALK_ERRONEOUS_EXP (EXP : TREE) return TREE is
    DUMMY_TYPESET    : TYPESET_TYPE;
    DUMMY_IS_SUBTYPE : Boolean;
  begin
    EVAL_EXP_SUBTYPE_TYPES (EXP, DUMMY_TYPESET, DUMMY_IS_SUBTYPE);
    return RESOLVE_EXP (EXP, TREE_VOID);
  end WALK_ERRONEOUS_EXP;

        ------------------------------------------------------------------------

  function WALK_DISCRMT_CONSTRAINT (RECORD_TYPE : TREE; GENERAL_ASSOC_S : TREE) return TREE is
    ACTUAL_COUNT    : Natural  := COUNT_AGGREGATE_CHOICES (GENERAL_ASSOC_S);
    AGGREGATE_ARRAY : AGGREGATE_ARRAY_TYPE (1 .. ACTUAL_COUNT);
    NORMALIZED_LIST : SEQ_TYPE := (TREE_NIL, TREE_NIL);
    LAST_POSITIONAL : Natural  := 0;
  begin
    SPREAD_ASSOC_S (GENERAL_ASSOC_S, AGGREGATE_ARRAY);
    WALK_RECORD_DECL_S (GENERAL_ASSOC_S, D (SM_DISCRIMINANT_S, RECORD_TYPE), AGGREGATE_ARRAY, NORMALIZED_LIST, LAST_POSITIONAL);
    RESOLVE_RECORD_ASSOC_S (GENERAL_ASSOC_S, AGGREGATE_ARRAY);
    return MAKE_EXP_S (LIST => NORMALIZED_LIST);
  end WALK_DISCRMT_CONSTRAINT;

    --|----------------------------------------------------------------------------------------------
end EXPRESO;
