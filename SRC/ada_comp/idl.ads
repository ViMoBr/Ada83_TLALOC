WITH DIANA_NODE_ATTR_CLASS_NAMES;
USE  DIANA_NODE_ATTR_CLASS_NAMES;
--|-------------------------------------------------------------------------------------------------
--|		PACKAGE IDL
--|-------------------------------------------------------------------------------------------------
PACKAGE IDL IS

  PROJECT_PATH		: STRING( 1..256 )	:= (OTHERS=>' ');				--| CHEMIN D ACCES AU REPERTOIRE PROJET QUI CONTIENT /ADA__LIB
  PROJECT_PATH_LENGTH	: NATURAL	:= 0;						--| LONGUEUR DU CHEMIN PROJET

  LIB_PATH		: STRING( 1..256 )	:= (OTHERS=>' ');				--| CHEMIN D ACCES AU REPERTOIRE LIBRAIRIE
  LIB_PATH_LENGTH		: NATURAL	:= 0;
  DEFAULT_LIB_PATH		: STRING( 1..10 )	:= "/ADA__LIB/";				--| CHEMIN D ACCES AU REPERTOIRE LIBRAIRIE

       	--| CREATION/OUVERTURE FERMETURE DE FICHIER DIANA
   
  PROCEDURE CREATE_IDL_TREE_FILE	( PAGE_FILE_NAME :STRING );				--| CREE UN FICHIER D'ARBRE DIANA
  PROCEDURE OPEN_IDL_TREE_FILE	( PAGE_FILE_NAME :STRING );				--| OUVRE UN FICHIER D'ARBRE DEJA EXISTANT
  PROCEDURE CLOSE_IDL_TREE_FILE;							--| FERMETURE DU FICHIER D'ARBRE
   
   	--| ETAPES FRONTALES DE COMPILATION
   
  PROCEDURE PAR_PHASE	( NOM_TEXTE, LIB_PATH :STRING );				--| ANALYSE SYNTAXIQUE DU FICHIER "NOM_TEXTE" (CHEMIN COMPLET), PRODUIT UN FICHIER D'ARBRE PARTIELLEMENT ATTRIBUE
  PROCEDURE LIB_PHASE;								--| INSERTION DES ELEMENTS DE LIBRAIRIE UTILISES DANS L'ARBRE DIANA
  PROCEDURE SEM_PHASE;								--| ANALYSE SEMANTIQUE DU FICHIER ARBRE DIANA
  PROCEDURE ERR_PHASE;								--| ECRITURE DES ERREURS POUR LES ANALYSES SUR L'ARBRE
  PROCEDURE WRITE_LIB;								--| MODIFICATION DES INFORMATIONS DE LIBRAIRIE SUITE A LA COMILATION DE "NOM_TEXTE.DIA"
  PROCEDURE PRETTY_DIANA	( OPTION :CHARACTER := 'U' );
      
   
   
  --| DISPOSITIF D'ACCES A UN ARBRE DIANA

   
  --| POUR UTILISER LES ELEMENTS ATTRIBUTE_NAME ET NODE_NAME ET LES CLASS_, IL FAUT SE REFERER AU DOCUMENT DE L'IDL DIANA
   
  TYPE SHORT			IS RANGE -32_768 .. 32767;	FOR SHORT'SIZE    USE 16;
  TYPE SSHORT			IS RANGE -32_768/2 .. 32767/2;FOR SSHORT'SIZE   USE 15;
  TYPE PAGE_IDX			IS RANGE 0 .. 16#7FFF#;	FOR PAGE_IDX'SIZE USE 15;
  TYPE LINE_IDX			IS RANGE 0 .. 127;		FOR LINE_IDX'SIZE USE 7;
  SUBTYPE ATTR_NBR			IS LINE_IDX;
  TYPE LINE_NBR			IS RANGE 0 .. 128;

  TYPE SRCCOL_IDX			IS RANGE 0 .. 255;		FOR SRCCOL_IDX'SIZE USE 8;
 
  TYPE VPTR_TYPE			IS (P, S, L, HI);					--| PTR NOEUD, SOURCE_POS, LIST, HEADER/INTEGER
  TYPE TREE (PT : VPTR_TYPE := P)	IS						--| PAR DEFAUt POINTEUR DE NOEUD
		RECORD
		  CASE PT IS
		  WHEN P | L =>							--| POINTEUR NORMAL DE NOEUD OU ATTRIBUT LISTE
		    PG		: PAGE_IDX;					--| REFERENCE DE PAGE VIRTUELLE
		    LN		: LINE_IDX;					--| DECALAGE DANS UNE PAGE VIRTUELLE
		    TY		: NODE_NAME;					--| TYPE DE NOEUD
		  WHEN S =>							--| POINTEUR DE SOURCE_LINE AVEC COLONNE SOURCE EN PLACE DU TYPE
		    SPG		: PAGE_IDX;					--| REFERENCE DE PAGE VIRTUELLE
		    SLN		: LINE_IDX;					--| DECALAGE DANS UNE PAGE VIRTUELLE
		    COL		: SRCCOL_IDX;					--| NUMERO DE COLONNE DANS LE TEXTE SOURCE
		  WHEN HI =>							--| HEADER DE NOEUD OU INTEGER (SHORT)
		    VALU		: SSHORT		:= 0;				--| VALEUR NUMERIQUE ENTIERE
		    NSIZ		: ATTR_NBR	:= 0;				--| NOMBRE D ATTRIBUTS DU NOEUD (SI ENTETE)
		    NOTY		: NODE_NAME	:= DN_NIL;			--| TYPE DE NOEUD
		  END CASE;
		END RECORD;
		FOR TREE'SIZE USE 32;
		FOR TREE USE RECORD AT MOD 4;
			PT	AT 0 RANGE 0..1;
			LN	AT 0 RANGE 2..8;
			SLN	AT 0 RANGE 2..8;
			NSIZ	AT 0 RANGE 2..8;
			PG	AT 0 RANGE 9..23;
			SPG	AT 0 RANGE 9..23;
			VALU	AT 0 RANGE 9..23;
			COL	AT 0 RANGE 24..31;
			TY	AT 0 RANGE 24..31;
			NOTY	AT 0 RANGE 24..31;
			END RECORD;

  TREE_NIL		: CONSTANT TREE	:= (P, TY => DN_NIL,   PG => 0, LN => 0);
  TREE_VOID		: CONSTANT TREE	:= (P, TY => DN_VOID,  PG => 0, LN => 0);	--| POINTEUR NON TYPE MAIS PAS NIL EN GENERAL (POINTE QUELQUE CHOSE)
  TREE_ROOT		: CONSTANT TREE	:= (P, TY => DN_ROOT,  PG => 1, LN => 0);
   
  TYPE SEQ_TYPE		IS RECORD
			  FIRST, NEXT	: TREE;
			END RECORD;
   
   	--| ACCES A L'ARBRE
   
  FUNCTION  MAKE		( NN :NODE_NAME )				RETURN TREE;	--| AJOUTE UN NOEUD DE TYPE NODE_NAME
   
  PROCEDURE D		( AN :ATTRIBUTE_NAME; T :TREE; V :TREE );			--| ECRITURE D'UN ATTRIBUT CONTENANT UN ARBRE
  FUNCTION  D		( AN :ATTRIBUTE_NAME; T :TREE )		RETURN TREE;	--| LECTURE D'UN ATTRIBUT CONTENANT UN ARBRE
   
  PROCEDURE DB		( AN :ATTRIBUTE_NAME; T :TREE; V :BOOLEAN );			--| ECRITURE D'UN ATTRIBUT CONTENANT UN BOOLEEN
  FUNCTION  DB		( AN :ATTRIBUTE_NAME; T :TREE )		RETURN BOOLEAN;	--| LECTURE D'UN ATTRIBUT CONTENANT UN BOOLEEN
   
  PROCEDURE DI		( AN :ATTRIBUTE_NAME; T :TREE; V :INTEGER );			--| ECRITURE D'UN ATTRIBUT CONTENANT UN ENTIER (16 BITS)
  FUNCTION  DI		( AN :ATTRIBUTE_NAME; T :TREE )		RETURN INTEGER;	--| LECTURE D'UN ATTRIBUT CONTENANT UN ENTIER (16 BITS)
   
  FUNCTION  LIST		( T :TREE )				RETURN SEQ_TYPE;	--| REND LA LISTE CONTENUE DANS UN NOEUD POINTE PAR LE POINTEUR D'ARBRE
  FUNCTION  IS_EMPTY	( S :SEQ_TYPE )				RETURN BOOLEAN;	--| TESTE UNE LISTE
  PROCEDURE POP		( S :IN OUT SEQ_TYPE; T :OUT TREE );				--| EXTRAIT UN ELEMENT DE LISTE ET REPOINTE S SUR LE RESTE
   
  FUNCTION  PRINT_NAME	( T :TREE )				RETURN STRING;	--| TXTREP OR SYMBOL_REP
  FUNCTION  NODE_IMAGE	( NN :NODE_NAME )				RETURN STRING;	--| CHAINE REPRESENTANT UN NOEUD
  FUNCTION  ATTR_IMAGE	( AN :ATTRIBUTE_NAME )			RETURN STRING;	--| CHAIUNE REPRESENTANT UN NOM D'ATTRIBUT

  FUNCTION  GET_LIB_PREFIX						RETURN STRING;	--| UTILISEE PAR LIB_PHASE ET WRITE_LIB

	--| AFFICHAGE DE TOUT OU PARTIE DE L'ARBRE
   	
  --|-----------------------------------------------------------------------------------------------
  --|		PRINT_NOD								--| POUR L'AFFICHAGE D'ELEMENTS D'ARBRE DIANA
  --|-----------------------------------------------------------------------------------------------
  PACKAGE PRINT_NOD IS
      
    PROCEDURE PRINT_TREE	( T :TREE );
    FUNCTION  L_PRINT_TREE	( T :TREE )		RETURN NATURAL;			--| RETOURNE LE NOMBRE DE CARACTERES
    PROCEDURE PRINT_NODE	( T :TREE; INDENT :NATURAL :=0 );
       
  --|-----------------------------------------------------------------------------------------------
  END PRINT_NOD;
   
   
   
  PRAGMA INLINE ( DB );
  PRAGMA INLINE ( DI );
  PRAGMA INLINE ( D );
   
--|-------------------------------------------------------------------------------------------------
END IDL;
