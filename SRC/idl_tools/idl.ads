--|-------------------------------------------------------------------------------------------------
--|		IDL								--| VERSION SPECIALISEE POUR LE TRAITEMENT DE GRAMMAIRE
--|-------------------------------------------------------------------------------------------------
PACKAGE IDL IS
    
       	--| CREATION/OUVERTURE FERMETURE DE FICHIER .LAR (FICHIER ARBRE)
   
  PROCEDURE CREATE_IDL_TREE_FILE	( PAGE_FILE_NAME :STRING );				--| CREE UN FICHIER "PAGE_FILE_NAME.LAR"
  PROCEDURE OPEN_IDL_TREE_FILE	( PAGE_FILE_NAME :STRING );				--| OUVRE UN FICHIER "PAGE_FILE_NAME.LAR" DEJÀ CREÉ
  PROCEDURE CLOSE_IDL_TREE_FILE;							--| FERMETURE DU FICHIER ".LAR"
   
      
   	--| UTILITAIRES IDL
   
  PROCEDURE IDL_READ	( NOM_TEXTE :STRING );					--| LIRE LA DESCRIPTION ET METTRE EN FICHIER ARBRE
  PROCEDURE NAM_PUT		( NOM_TEXTE :STRING );					--| ECRIRE LES TYPES ENUMERES
  PROCEDURE TBL_PUT		( NOM_TEXTE :STRING );					--| ECRIRE LE FICHIER .TBL
   
   
	--| DISPOSITIF D'ACCES A UN ARBRE REPESENTANT UNE DESCRIPTION IDL
   
  TYPE ATTRIBUTE_NAME IS (
        	XD_HIGH_PAGE,	XD_USER_ROOT,	XD_SOURCE_LIST,	XD_ERR_COUNT, 	SPARE_1,
	XD_HEAD,		XD_TAIL,		XD_NUMBER,	XD_ERROR_LIST, 	XD_SRCPOS,
	XD_TEXT,		XD_DEFLIST,	XD_LIST,		XD_SOURCENAME,	XD_RULE_S,
	XD_SYMREP,	LX_SRCPOS,	XD_RIGHT_SIDE,	XD_IS_CLASS,      	XD_PARENT,
	XD_NODE_ID,	XD_ATTR_ID,	XD_ATTR_TYPE,	XD_CLASS_NODE
      	);
   
  TYPE NODE_NAME IS (
      	DN_ROOT,		DN_TXTREP,	DN_NUM_VAL,      	DN_FALSE,		DN_TRUE,
	DN_NIL,		DN_LIST,      	DN_SOURCELINE,	DN_ERROR,		DN_SYMBOL_REP,
	DN_HASH,      	DN_VOID,		DN_USER_ROOT,	DN_CLASS_NODE,	DN_ATTR,
      	DN_MEMBER,	DN_VIRGIN
      	);
	FOR NODE_NAME'SIZE USE 8;
   
  TYPE SHORT			IS RANGE -32_768 .. 32767;	FOR SHORT'SIZE    USE 16;
  TYPE PAGE_IDX			IS RANGE 0 .. 16#7FFF#;	FOR PAGE_IDX'SIZE USE 15;
  TYPE LINE_IDX			IS RANGE 0 .. 127;		FOR LINE_IDX'SIZE USE 7;
  SUBTYPE ATTR_NBR			IS LINE_IDX;
  TYPE LINE_NBR			IS RANGE 0 .. 128;

  TYPE SRCCOL_IDX			IS RANGE 0 .. 255;		FOR SRCCOL_IDX'SIZE USE 8;
 
  TYPE VPTR_TYPE			IS (N, S, L, F);					--| NOEUD, SOURCE_POS, LIST, FINAL
  TYPE TREE (PT : VPTR_TYPE := N)	IS						--| TYPE FONDAMENTAL REMPLISSANT LES BLOCS D ARBRE
		RECORD
		  CASE PT IS
		  WHEN N | L =>							--| POINTEUR NORMAL DE NOEUD OU ATTRIBUT LISTE
		    PG		: PAGE_IDX;					--| REFERENCE DE PAGE VIRTUELLE
		    LN		: LINE_IDX;					--| DECALAGE DANS UNE PAGE VIRTUELLE
		    TY		: NODE_NAME;					--| TYPE DE NOEUD
		  WHEN S =>							--| POINTEUR DE SOURCE_LINE AVEC COLONNE SOURCE EN PLACE DU TYPE
		    SPG		: PAGE_IDX;					--| REFERENCE DE PAGE VIRTUELLE
		    SLN		: LINE_IDX;					--| DECALAGE DANS UNE PAGE VIRTUELLE
		    COL		: SRCCOL_IDX;					--| NUMERO DE COLONNE DANS LE TEXTE SOURCE
		  WHEN F =>							--| POINTEUR NORMAL DE NOEUD OU ATTRIBUT LISTE
		    VAL		: SHORT;						--| VALEUR NUMERIQUE ENTIERE
		    TTY		: NODE_NAME;					--| TYPE DE NOEUD
		  END CASE;
		END RECORD;
		FOR TREE'SIZE USE 32;
		FOR TREE USE RECORD AT MOD 4;
			PT	AT 0 RANGE 0..1;
			LN	AT 0 RANGE 2..8;
			SLN	AT 0 RANGE 2..8;
			PG	AT 0 RANGE 9..23;
			SPG	AT 0 RANGE 9..23;
			COL	AT 0 RANGE 24..31;
			TY	AT 0 RANGE 24..31;
			TTY	AT 0 RANGE 24..31;
			VAL	AT 0 RANGE 2..17;
			END RECORD;

  TREE_NIL		: CONSTANT TREE	:= (N, 0, 1, DN_NIL);
   
  TYPE SEQ_TYPE		IS RECORD
			  FIRST, NEXT	: TREE;
			END RECORD;
   
   	--| ACCES A L'ARBRE
   
  FUNCTION  MAKE		( NN :NODE_NAME )				RETURN TREE;	--| AJOUTE UN NOEUD DE TYPE NODE_NAME
   
  PROCEDURE D		( AN :ATTRIBUTE_NAME; T :TREE; V :TREE );			--| ECRITURE D'UN ATTRIBUT CONTENANT UN ARBRE
  FUNCTION  D		( AN :ATTRIBUTE_NAME; T :TREE )		RETURN TREE;	--| LECTURE D'UN ATTRIBUT CONTENANT UN ARBRE
   
  PROCEDURE DB		( AN :ATTRIBUTE_NAME; T :TREE; V :BOOLEAN );			--| ECRITURE D'UN ATTRIBUT CONTENANT UN BOOLEEN
  FUNCTION  DB		( AN :ATTRIBUTE_NAME; T :TREE )		RETURN BOOLEAN;	--| LECTURE D'UN ATTRIBUT CONTENANT UN BOOLEEN
   
  PROCEDURE DI		( AN :ATTRIBUTE_NAME; T :TREE; V :INTEGER );			--| ECRITURE D'UN ATTRIBUT CONTENANT UN ENTIER (16 BITS)
  FUNCTION  DI		( AN :ATTRIBUTE_NAME; T :TREE )		RETURN INTEGER;	--| LECTURE D'UN ATTRIBUT CONTENANT UN ENTIER (16 BITS)
   
  FUNCTION  LIST		( T :TREE )				RETURN SEQ_TYPE;	--| REND LA LISTE CONTENUE DANS UN NOEUD POINTE PAR LE POINTEUR D'ARBRE
  FUNCTION  IS_EMPTY	( S :SEQ_TYPE )				RETURN BOOLEAN;	--| TESTE UNE LISTE
  PROCEDURE POP		( S :IN OUT SEQ_TYPE; T :OUT TREE );				--| EXTRAIT UN ELEMENT DE LISTE ET REPOINTE S SUR LE RESTE
   
  FUNCTION  PRINT_NAME	( T :TREE )				RETURN STRING;	--| TXTREP OR SYMBOL_REP
  FUNCTION  NODE_IMAGE	( NN :NODE_NAME )				RETURN STRING;	--| CHAINE REPRESENTANT UN NOEUD
  FUNCTION  ATTR_IMAGE	( AN :ATTRIBUTE_NAME )			RETURN STRING;	--| CHAIUNE REPRESENTANT UN NOM D'ATTRIBUT

	--| AFFICHAGE DE TOUT OU PARTIE DE L'ARBRE
   	
  --|-----------------------------------------------------------------------------------------------
  --|		PRINT_NOD								--| POUR L'AFFICHAGE D'ELEMENTS D'ARBRE DIANA
  --|-----------------------------------------------------------------------------------------------
  PACKAGE PRINT_NOD IS
      
    PROCEDURE PRINT_TREE	( T :TREE );
    FUNCTION  L_PRINT_TREE	( T :TREE )		RETURN NATURAL;			--| RETOURNE LE NOMBRE DE CARACTERES
    PROCEDURE PRINT_NODE	( T :TREE; INDENT :NATURAL :=0 );
       
  --|-----------------------------------------------------------------------------------------------
  END PRINT_NOD;
   
   
   
  PRAGMA INLINE ( DB );
  PRAGMA INLINE ( DI );
  PRAGMA INLINE ( D );
   
--|-------------------------------------------------------------------------------------------------
END IDL;
